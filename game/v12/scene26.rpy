# SCENE 26: At the hotel lobby 
# Locations: Hotel lobby
# Characters: RYAN (Outfit: 1), MC (Outfit: 3), NORA (Outfit: 1), CHRIS (Outfit: 1), CHLOE (Outfit: 1), AUBREY (Outfit: 2)
# Time: Morning
# Phone Images: NONE

label v12_nora_chris_fight:
    scene v12ncf1 # TPP. Show MC walking towards Ryan in the lobby, Ryan waving at MC, slight smile, mouth closed, MC slight smile, mouth closed
    with fade

    pause 0.75

    play music music.v12_Track_Scene_26 fadein 2

    scene v12ncf2 # FPP. MC and Ryan standing in front of each other in the lobby, Ryan slight smile, mouth open
    with dissolve

    ry "What's up, [name]?"

    scene v12ncf2a # FPP. Same as v12ncf2, Ryan slight smile, mouth closed
    with dissolve

    u "Not much man, just starting the-"

    scene v12ncf3 # TPP. Nora and Chris fighting in the hotel lobby. Show Nora looking at Chris, Nora very angry, mouth open (only Nora in shot, make sure they are near a wall)
    with vpunch

    no "SO YOU LIED TO ME!"

    scene v12ncf4 # TPP. Same positioning as v12ncf3, Show both Nora and Chris looking very angry at each other, both mouths closed, they are having a heated argument
    with dissolve

    u "(Oh shit!)"

    scene v12ncf5 # TPP. Same positioning as v12ncf3, Show Chris looking at Nora, Chris very angry, mouth open (only Chris in shot)
    with dissolve

    ch "I didn't lie, Nora. I was busy, and now I'm not. So, I planned a night with the guys. I don't see what's so bad about that."

    scene v12ncf3
    with dissolve

    no "You don't- ARE YOU A FUCKING IDIOT?!"

    scene v12ncf3a # TPP. Same as v12ncf3, different pose
    with dissolve

    no "I asked you to hang out tonight, and you told me you were busy. Now you realize you're free, and instead of making plans with me YOU CALL THE FUCKING GUYS!?"

    scene v12ncf3
    with dissolve

    no "WHO YOU JUST WENT OUT WITH! THE OTHER FUCKING NIGHT!"

    scene v12ncf5
    with dissolve

    ch "Honestly, Nora... How could I want to hang out with you when you're acting like this?"

    scene v12ncf3
    with dissolve

    no "ARE YOU FUCKING SERIOUS RIGHT NOW!?"

    scene v12ncf4a # TPP. Same as v12ncf4, Nora pushing Chris' chest, Nora very angry, mouth open, Chris startled, mouth closed
    with dissolve

    no "WHO WOULD WANNA HANG OUT WITH ME?!"

    scene v12ncf4b # TPP. Same as v12ncf4a, Nora pushing Chris again, Nora very angry, mouth open, Chris angry, mouth closed
    with dissolve

    no "MAYBE MY FUCKING BOYFRIEND!"

    scene v12ncf4
    with dissolve

    u "(This is getting pretty bad.)"

    scene v12ncf5
    with dissolve

    ch "*Stern* Don't put your hands on me."

    scene v12ncf4c # TPP. Same as v12ncf4b, Nora pushing Chris' shoulder, Nora very angry, mouth open, Chris angry, mouth closed
    with dissolve

    no "I'LL DO WHATEVER THE FUCK I WANT, SINCE YOU DO WHATEVER YOU WANT!"

    scene v12ncf6 # TPP. Show Chloe and Aubrey watching the fight from a distance, both worried, mouths closed
    with dissolve

    pause 1.25

    scene v12ncf5a # TPP. Same as v12ncf5, Chris very angry, mouth open, pointing his finger at Nora
    with dissolve

    ch "*Very stern* I'm not gonna say it again, Nora. Don't put your hands on me."

    scene v12ncf4d # TPP. Same as v12ncf4c, Nora grabbing Chris' jacket, Chris very angry, mouth closed, Nora very angry, mouth open
    with dissolve

    no "Maybe if you weren't so focused on chasing after the man your daddy was, you'd be your own-"

    play sound sound.hit
    scene v12ncf4e # TPP. Same as v12ncf4, Chris pushing Nora, Chris very angry, mouth closed, Nora losing her balance, mouth closed, scared
    with hpunch

    pause 1.25

    play sound sound.fall
    scene v12ncf7 # TPP. Nora on the ground, mouth open, curled up on the ground, crying, Chris looking down at her, Chris very angry, mouth closed
    with vpunch

    no "*Crying* Ahh!"

    scene v12ncf8 # TPP. Show MC very worried walking towards Nora, Ryan holding him back, Ryan worried, both mouths closed
    with dissolve

    pause 1

    scene v12ncf6a # TPP. Show Aubrey and Chloe running over to Nora, both of them very worried, mouths closed
    with dissolve

    pause 1

    scene v12ncf9 # TPP. Show Chloe in front of Chris, Chloe very angry, mouth open (Only Chris in shot)
    with dissolve

    cl "What the fuck is wrong with you?!"

    scene v12ncf10 # TPP. Same positioning as v12ncf9, Chris angry, mouth open, looking at Chloe (Only Chris in shot)
    with dissolve

    ch "You didn't say shit when she was hitting me! I swear nobody gives a fuck when a man's getting hit, but the minute a man defends himself against a woman it's game fucking over."

    scene v12ncf9
    with dissolve

    cl "Because she wasn't hurting you and you pushed her hard! Are you fucking stupid?!"

    scene v12ncf10
    with dissolve

    ch "I... I didn't mean to push her that hard. I told her to stop putting her hands on me. Twice."

    scene v12ncf9
    with dissolve

    cl "SO!? THAT DOESN'T GIVE YOU THE RIGHT TO SHOVE HER LIKE THAT! YOU'RE A GROWN ASS MAN!"

    scene v12ncf11 # TPP. Show Aubrey kneeling down next to Nora, Nora crying, mouth closed, Aubrey worried, mouth open, both of them looking at each other
    with dissolve

    au "Are you okay?!"

    scene v12ncf11a # TPP. Same as v12ncf11, Aubrey worried, mouth closed, Nora crying, mouth open
    with dissolve

    no "*Crying* I'm fine."

    scene v12ncf11b # TPP. Same as v12ncf11, Show Aubrey helping Nora up, Nora crying, mouth closed, Aubrey worried, mouth closed
    with dissolve

    pause 0.75

    scene v12ncf5b # TPP. Same as v12ncf5, Chris worried, mouth open, he is apologizing
    with dissolve

    ch "Nora, I... I didn't mean to push you that hard. I'm sorry."

    scene v12ncf12 # TPP. Show Nora walking away from Chris, Nora crying, mouth open, Chris very worried, mouth closed
    with dissolve

    no "*Sobbing* Leave me alone."

    scene v12ncf13 # TPP. Show Chris pacing in front of the wall (going to the left), very worried, mouth closed
    with dissolve

    ch "FUCK! FUCK! FUCK!"

    scene v12ncf13a # TPP. Same as v12ncf13, Chris pacing to the right, very worried, mouth closed
    with dissolve

    pause 0.75

    scene v12ncf13b # TPP. Same as v12ncf13a, Chris punching the wall very hard, Chris very worried, mouth closed
    with vpunch

    pause 0.75

    scene v12ncf13c # TPP. Same as v12ncg13, Chris holding his wrist, looking down at it, Chris angry, mouth open
    with dissolve

    ch "Bro, fuck all this shit!"

    scene v12ncf14 # TPP. Show Chris going to walk out of the hotel, mouth closed, angry, holding his wrist, Nora walking towards the hotel rooms, Nora crying, mouth closed
    with dissolve

    pause 0.75

    menu:
        "Go to Nora":
            $ reputation.add_point(RepComponent.BOYFRIEND)
            $ v12_followed_nora = True

            stop music fadeout 3
            jump v12_chase_nora #scene 26b

        "Go to Chris":
            $ v12_help_chris += 1
            $ reputation.add_point(RepComponent.BRO)

            stop music fadeout 3
            jump v12_follow_chris #scene 26a