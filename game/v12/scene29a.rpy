# SCENE 29a: Late Night Workout in the Gym with Lindsey
# Locations: Hotel, gym
# Characters: LINDSEY (Outfit: 1), MC (Outfit: 3)
# Time: Night
# Phone Images: 

label v12_late_night_workout:
    scene v12lnw1 # TPP Show MC in hotel lobby, sitting by himself, Lindsey walking up to him and waving, Lindsey with mouth open
    with dissolve

    li "What are you doing down here all by yourself?"

    play music music.ck1.v12.Track_Scene_29_1 fadein 2

    if mc.frat == Frat.WOLVES:
        scene v12lnw2 # FPP Show Lindsey, looking at MC, neutral expression with mouth closed
        with dissolve

        u "I just got back from hanging out with Imre."

    else:
        scene v12lnw2
        with dissolve

        u "I just got back from hanging out with Ryan."

    scene v12lnw2a # FPP Same angle as v12lnw2, Lindsey with mouth open
    with dissolve

    li "Gotcha, gotcha. Are you getting ready to go to bed?"

    scene v12lnw2
    with dissolve

    u "I'm not sure what I'm doing. Today's been pretty impactful, but I'm honestly not even tired."

    scene v12lnw2a
    with dissolve

    li "Want to go to the gym with me?"

    scene v12lnw2
    with dissolve

    u "Are the gyms even open? It's super late. Plus, I'm not in gym clothes."

    scene v12lnw2a
    with dissolve

    li "They're twenty-four hours, I checked. I wanna go blow off some steam, but I'd rather not go out this late by myself. And you're dressed fine."

    scene v12lnw2
    with dissolve

    u "So you want big and strong [name] to accompany you so you don't have to go by yourself?"

    scene v12lnw2b # FPP Same angle as v12lnw2, Linsdsey smiling with mouth open
    with dissolve

    li "*Chuckles* It'd be nice if you would."

    scene v12lnw1a # TPP Same angle as v12lnw1, MC getting to his feet and looking at Lindsey, who is standing nearby with her mouth closed
    with dissolve

    pause 0.75

    scene v12lnw3 # FPP Show Lindsey, looking at MC who is now standing, Lindsey smiling with mouth closed
    with dissolve

    u "*Sighs* C'mon then."

    scene v12lnw3a # FPP Same angle as v12lnw3, Lindsey smiling with mouth open
    with dissolve

    li "You'll really go with me?"

    scene v12lnw3
    with dissolve

    u "Yeah."

    scene v12lnw3a
    with dissolve

    li "Oh my gosh, thanks. I just knew you were gonna say no."

    scene v12lnw3
    with dissolve

    u "Why would I say no?"

    scene v12lnw3a
    with dissolve

    li "Well, like you said, it's really late."

    scene v12lnw3
    with dissolve

    u "Lucky you, I'm not tired."

    scene v12lnw3a
    with dissolve

    li "Haha, lucky me."

    scene v12lnw4 # TPP Show MC and Lindsey walking out of hotel lobby into the night
    with dissolve

    pause 0.75

    scene v12lnw5 # TPP Show MC and Lindsey arriving at gym, which has lights on inside
    with dissolve

    pause 0.75

    stop music fadeout 3
    play music music.ck1.v12.Track_Scene_29a_1 fadein 2

    scene v12lnw6 # TPP Show MC and Lindsey, now inside gym looking around, Lindsey looks amazed and has mouth open
    with dissolve

    li "Oh wow, this is so nice. This is nothing like what I expected."

    scene v12lnw7 # FPP Show Lindsey, who is still looking around the gym, smiling with mouth closed
    with dissolve

    u "*Chuckles* You look like a kid at a chocolate factory. This is no different than any other gym really."

    scene v12lnw7a # FPP Same angle as v12lnw7, Lindsey still looking away, smiling with mouth open
    with dissolve

    li "I wouldn't know."

    scene v12lnw7
    with dissolve

    u "What do you mean you wouldn't know?"

    scene v12lnw7b # FPP Same angle as v12lnw7, Lindsey looking at MC, smiling with mouth open
    with dissolve

    li "I haven't actually worked out at a real gym before."

    scene v12lnw7c # FPP Same angle as v12lnw7, Lindsey looking at MC, smiling with mouth closed
    with dissolve

    u "You don't work out at all?"

    scene v12lnw7b
    with dissolve

    li "Yeah, but I usually just do at-home stuff. I don't have any of my stuff with me here and I thought it'd be fun to actually go to a gym for once."

    scene v12lnw7c
    with dissolve

    u "Have you ever used any of this stuff, like the punching bag or bench press?"

    scene v12lnw7b
    with dissolve

    li "Nope, I'm more of a yoga ball and dumbbell kind of girl."

    scene v12lnw7c
    with dissolve

    u "Haha, okay. Well, we're gonna change that today. What do you wanna try first?"

    scene v12lnw7a
    with dissolve

    li "Hmm, can we try the bench press?"

    scene v12lnw7
    with dissolve

    u "We sure can."

    scene v12lnw8 # TPP Show MC and Lindsey walking over to the bench press
    with dissolve

    pause 0.75

    scene v12lnw9 # FPP Show Lindsey looking down at bench press, neutral expression, mouth closed
    with dissolve

    u "Okay, do know how to use it?"

    scene v12lnw9a # FPP Same angle as v12lnw9, Lindsey looking at MC, mouth open
    with dissolve

    li "Yeah, it seems pretty simple, but can you go first?"

    scene v12lnw9b # FPP Same angle as v12lnw9, Lindsey looking at MC, mouth closed
    with dissolve

    u "Haha, sure, but now that I think about it we should probably stretch first."

    scene v12lnw9a
    with dissolve

    li "Good idea."

    scene v12lnw10 # TPP MC and Lindsey moving to a space in front of large, floor-to-ceiling mirrors
    with dissolve

    pause 1.25

    scene v12lnw10a # TPP Same angle as v12lnw10, MC attempting to touch his toes but can't reach, Lindsey, getting into position on the floor
    with dissolve

    pause 1.25

    scene v12lnw11 # FPP Lindsey laying on her back on the ground, pulling one knee to her chest, her other leg lifting slightly off the ground
    with dissolve

    li "Do you mind holding my leg down?"

    scene v12lnw11a # FPP Same angle as v12lnw11, Lindsey's mouth closed
    with dissolve

    u "Yeah, I got you."

    scene v12lnw12 # FPP View of MC as he holds down Lindsey's leg while she holds her other knee to her chest, Lindsey's mouth open
    with dissolve

    li "Thanks, can we do the other one now?"

    scene v12lnw12a # FPP Same angle as v12lnw12, Lindsey's mouth closed
    with dissolve

    u "Yeah."

    scene v12lnw12b # FPP Same angle as v12lnw12, Lindsey's switched which leg she is stretching, mouth closed
    with dissolve

    pause 1.25

    scene v12lnw13 # TPP MC getting to his feet and holding a hand down to help Lindsey up
    with dissolve

    pause 1.25

    scene v12lnw14 # FPP Show Lindsey, standing near mirror, looking at MC, smiling with mouth open
    with dissolve

    li "Alright, I feel good. How about you?"

    scene v12lnw14a # FPP Same angle as v12lnw14, Lindsey smiling with mouth closed
    with dissolve

    u "I'm good."

    scene v12lnw14
    with dissolve

    li "Nahhh, I don't think so."

    scene v12lnw14a
    with dissolve

    u "Haha, what makes you say that?"

    scene v12lnw14
    with dissolve

    li "I saw you trying to stretch your calves and so forth by touching your toes, but you couldn't and gave up pretty fast."

    scene v12lnw14a
    with dissolve

    u "Well I'm not gonna break my back to touch my toes. *Chuckles*"

    scene v12lnw14
    with dissolve

    li "*Sighs* Boys are so silly when it comes to stretching. C'mere."

    scene v12lnw10b # TPP Same angle as v12lnw10, Lindsey leading MC to the mirror wall
    with dissolve

    pause 0.75

    scene v12lnw15 # FPP Lindsey looking at MC, neutral expression, mouth open
    with dissolve

    li "Okay, put your butt against the wall and make sure your heels are all the way back."

    scene v12lnw10c # TPP Same angle as v12lnw10, MC with his back and heels up against the wall
    with dissolve

    pause 1.25

    scene v12lnw15a # FPP Same angle as v12lnw15, Lindsey with neutral expression, mouth closed
    with dissolve

    u "Alright, now what?"

    scene v12lnw15
    with dissolve

    li "Listen carefully or else we'll be here all day. You need to breathe, stretch down and hold. The wall will keep you from bending your legs and I'll support your back as you lean."

    scene v12lnw15a
    with dissolve

    u "You making this sound frightening. *Chuckles*"

    scene v12lnw15b # FPP Same angle as v12lnw15, Lindsey smiling with mouth open
    with dissolve

    li "You'll be fine. c'mon."

    scene v12lnw10c
    with dissolve

    # -MC needs to make the choices in the correct order, breath, stretch, hold...if he messes up the order then Lindsey tells him "start over". This continues until he does it right, each choice is timed
    label v12_gym_stretch:
        menu (fail_label="v12s29a_faillabel1"):
            "Stretch":
                label v12s29a_faillabel1:
                    scene v12lnw10d # TPP Same angle as v12lnw10, MC against wall, stretching down to touch his toes, not reaching, leaning forward, Lindsey standing by
                    with dissolve

                    pause 1

                    scene v12lnw15
                    with dissolve

                    li "Start over."

                    jump v12_gym_stretch

            "Breathe":
                scene v12lnw15a
                with dissolve

                u "*Deep breath*"

                scene v12lnw15b
                with dissolve

                li "Good."

        menu (fail_label="v12s29a_faillabel2"):
            "Stretch":
                scene v12lnw10e # TPP Same angle as v12lnw10, MC against wall, stretching down to touch his toes, not reaching yet, Lindsey's hand on his back
                with dissolve

                pause 1

                scene v12lnw10f # TPP Same angle as v12lnw10, MC against wall, reaching down and just touching his toes, Lindsey's hand on his back
                with dissolve
                
                li "Perfect."

            "Lean":
                label v12s29a_faillabel2:
                    scene v12lnw10d
                    with dissolve

                    li "Start over"

                    jump v12_gym_stretch

        menu (fail_label="v12s29a_faillabel3"):
            "Hold":
                scene v12lnw10f
                with dissolve

                li "That's it, you did it."

                scene v12lnw15c # FPP Same angle as v12lnw15, Lindsey smiling with mouth closed
                with dissolve

                u "Finally. *Chuckles*"

            "Breathe":
                label v12s29a_faillabel3:
                    scene v12lnw15
                    with dissolve

                    li "Start over."

                    jump v12_gym_stretch

    scene v12lnw15b
    with dissolve

    li "Now you're ready to go. Bench press?"

    scene v12lnw15c
    with dissolve

    u "Bench press."

    scene v12lnw8
    with dissolve

    pause 0.75

    menu:
        "Show off":
            $ reputation.add_point(RepComponent.TROUBLEMAKER)

            scene v12lnw9b
            with dissolve

            u "Now I'm a professional so I'll go ahead start with some weight, but it's best you just use the bar. There's fifty pounds on here so that feel like a good start."

            scene v12lnw9c # FPP Same angle as v12lnw9, Lindsey smiling with mouth open
            with dissolve

            li "Okay Bulk Bogan! *Chuckles*"

            scene v12lnw16 # TPP Show MC laying down on bench press, ends of bar with plates out of frame
            with dissolve

            pause 0.75
            
            # MIGHT NEED TO PLAY WITH TIMING OF THESE BENCH PRESS SCENES SO IT DOESN'T FEEL TOO LONG
            scene v12lnw16a # TPP Same angle as v12lnw16, MC laying on bench press, hands gripping bar which is on the rests
            with dissolve

            pause 0.5

            scene v12lnw16b # TPP Same angle as v12lnw16, MC laying on bench press holding bar, bar lowered to his chest
            with dissolve

            pause 0.5

            # up rep 1
            scene v12lnw16c # TPP Same angle as v12lnw16, MC laying on bench press holding bar, arms extended up
            with dissolve

            pause 0.5

            scene v12lnw16b
            with dissolve

            pause 0.5

            # up rep 2
            scene v12lnw16c
            with dissolve

            pause 0.5

            scene v12lnw16b
            with dissolve

            pause 0.5

            # up final rep
            scene v12lnw16a
            with dissolve

            pause 0.75

            scene v12lnw17 # FPP MC laying on bench press, looking up at Lindsey, who is smiling with mouth closed
            with dissolve

            u "Too easy, Go ahead and add the twenty fives from that bench over there."

            scene v12lnw17a # FPP Same angle as v12lnw17, Lindsey smiling with mouth open
            with dissolve

            li "You sure?"

            scene v12lnw17
            with dissolve

            u "Positive!"

            scene v12lnw18 # TPP Show Lindsey placing a 25 pound plate on one end of the bench press bar, 25 pound plate is already on the other side
            with dissolve

            pause 0.75

            scene v12lnw17
            with dissolve

            u "Watch and learn."

            scene v12lnw16a
            with dissolve

            pause 0.5

            scene v12lnw16d # TPP Same angle as v12lnw16, MC visibly struggling to lift the heavier bar, which is laying on his chest
            with dissolve

            pause 0.75

            scene v12lnw17b # FPP Same angle as v12lnw17, Lindsey looking worried with mouth closed
            with dissolve

            u "SPOT! SPOT! SPOT!"

            scene v12lnw17c # FPP Same angle as v12lnw17, Lindsey looking worried with mouth open
            with dissolve

            li "Help me get it up!"

            scene v12lnw19 # TPP Show Lindsey straddling MC over the bench, Lindsey's face close to MC's, Lindsey grabbing bar and trying to lift it
            with dissolve

            pause 0.75

            scene v12lnw19a # TPP Same angle as v12lnw19, Lindsey still straddling MC, but they have managed to get the bar back on the rests
            with dissolve

            pause 0.75

            scene v12lnw20 # FPP MC looking up at Lindsey, who is still straddling him, looking down at him, worried with her mouth closed.
            with dissolve

            u "(Never had a spot like this. *Chuckles*)"

            scene v12lnw20a # FPP Same angle as v12lnw20, Lindsey looking worried with mouth open
            with dissolve

            li "Are you okay?"

            scene v12lnw20
            with dissolve

            u "Yeah yeah, I'm good. Should've worked my way up to that."

            scene v12lnw19b # FPP Same angle as v12lnw19, Lindsey getting off of MC, MC sitting up on the bench
            with dissolve

            pause 0.75

            scene v12lnw21 # FPP MC sitting on bench looking at Lindsey. Lindsey with neutral expression, mouth closed
            with dissolve

            u "You've never seen someone spot before have you? *Chuckles*"

            scene v12lnw21a # FPP Same angle as v12lnw21, Lindsey with mouth open
            with dissolve

            li "I didn't even know what that meant."

            scene v12lnw19c # TPP Same angle as v12lnw19, MC sitting on bench, entering text into his phone
            with dissolve

            pause 1.25

            scene v12lnw21b # FPP Same angle as v12lnw21, MC holding his phone out to show Lindsey, Lindsey's mouth closed
            with dissolve

            u "This is spotting."

            scene v12lnw21c # FPP Same angle as v12lnw21, Lindsey with an embarrassed smile, mouth open
            with dissolve

            li "Oh, I wasn't supposed to climb on top of you like that was I?"

            scene v12lnw21d # FPP Same angle as v12lnw21, Lindsey with embarrassed smile, mouth closed
            with dissolve

            u "Haha, no not really."

            scene v12lnw21c
            with dissolve

            li "My bad."

            scene v12lnw21d
            with dissolve

            menu:
                "It's fine":
                    u "You're fine, it was funny."

                    scene v12lnw21e # FPP Same angle as v12lnw21, Lindsey with big, genuine smile, mouth open
                    with dissolve

                    li "*Chuckles*"
                
                "Best spot ever":
                    $ reputation.add_point(RepComponent.TROUBLEMAKER)

                    scene v12lnw21d
                    #with dissolve

                    u "Don't apologize, that was the best spot I ever had."

                    scene v12lnw21e
                    with dissolve

                    li "*Chuckles* I'm sure it was."

            scene v12lnw19d # TPP Same angle as v12lnw19, MC standing up from bench press
            with dissolve

            pause 0.75

        "Basics":
            scene v12lnw9b
            with dissolve

            u "I wanna just show you the basics so let's take these weights off."

            scene v12lnw9a
            with dissolve

            li "Okay."

            scene v12lnw18a # TPP Same angle as v12lnw18, MC and Lindsey each removing a weight from the bench press bar
            with dissolve

            pause 0.75

            scene v12lnw16
            with dissolve

            pause 0.75

            scene v12lnw17
            with dissolve

            u "You always wanna spot the person just in case they can't carry the weight. So watch carefully, I won't struggle with just the bar, but just for practice be sure to pay attention."

            scene v12lnw17a
            with dissolve

            li "Okay."

            # SAME AS ABOVE, MIGHT WANT TO PLAY WITH THESE TIMINGS SO THIS BIT DOESN'T FEEL TOO LONG
            scene v12lnw16a
            with dissolve

            pause 0.5

            scene v12lnw16b
            with dissolve

            pause 0.5

            # up rep 1
            scene v12lnw16c
            with dissolve

            pause 0.5

            scene v12lnw16b
            with dissolve

            pause 0.5

            # up rep 2
            scene v12lnw16c
            with dissolve

            pause 0.5

            scene v12lnw16b
            with dissolve

            pause 0.5

            # up final rep
            scene v12lnw16a
            with dissolve

            pause 0.5

            scene v12lnw17
            with dissolve

            u "Easy."

            scene v12lnw19d
            with dissolve

            pause 0.75

# -Continued regardless of choice
    scene v12lnw9a
    with dissolve

    li "How much does the bar weigh?"

    scene v12lnw9
    with dissolve

    u "The bar is forty-five pounds."

    scene v12lnw9c
    with dissolve

    li "Okay yeah, I can only do the bar. When I use dumbbells my max is twenty pounds in each hand."

    scene v12lnw9b
    with dissolve

    u "Haha, alright then."

    scene v12lnw22 # FPP View of MC behind bench to spot. Show Lindsey laying down on the bench press and gripping the bar, still on its rests, Lindsey's mouth closed
    with dissolve

    u "Ready?"

    scene v12lnw22a # FPP Same angle as v12lnw22, Lindsey's mouth open
    with dissolve

    li "I hope so."

    scene v12lnw22
    with dissolve

    u "Alright, let's do it. I got you."

    scene v12lnw22b # FPP Same angle as v12lnw22, Lindsey has lowered the bar to her chest
    with dissolve

    pause 0.5

    scene v12lnw22c # FPP Same angle as v12lnw22, Lindsey has pushed the bar up until her arms are straight
    with dissolve

    pause 0.5

    scene v12lnw22b
    with dissolve

    pause 0.5

    scene v12lnw22d # FPP Same angle as v12lnw22, Lindsey struggling to lift the bar, Lindsey looking strained with mouth open
    with dissolve

    li "Oh god!"

    scene v12lnw22e # FPP Same angle as v12lnw22, MC has grabbed the bar to help Lindsey, bar is now on its rests, both holding it
    with dissolve

    pause 0.75

    scene v12lnw22f # FPP Same angle as v12lnw22, Lindsey has released the bar and let her arms flop to her sides, she is smiling with her mouth open
    with dissolve

    li "That is not forty-five pounds!"

    scene v12lnw22g # FPP Same angle as v12lnw22, Lindsey with her arms flopped to her sides, exhausted, smiling with mouth closed
    with dissolve

    u "*Laughs* I promise it is."

    scene v12lnw22f
    with dissolve

    li "Well, I won't be messing with those kinds of weights anytime soon. That made me tired."

    scene v12lnw22g
    with dissolve

    u "Haha, do you wanna do anything else or are you ready to go?"

    scene v12lnw19e # TPP Same angle as v12lnw19, Lindsey standing up from bench press, her mouth open, MC coming around from behind bench
    with dissolve

    li "Can we try the kicking bag?"

    scene v12lnw9b
    with dissolve

    u "You mean the punching bag? *Chuckles*"

    scene v12lnw9c
    with dissolve

    li "What, no. I mean the kicking bag, why would it be called a punching bag if people kick it?"

    scene v12lnw9d # FPP Same angle as v12lnw9, Lindsey smiling with mouth closed
    with dissolve

    u "The question should be, why isn't it just called a bag since people punch and kick it. *Chuckles*"

    scene v12lnw9c
    with dissolve

    li "Well I wanna kick it, try out this karate kick I saw in a movie."

    scene v12lnw9d
    with dissolve

    u "I gotta see this."

    scene v12lnw23 # TPP Show MC and Lindsey walking to the punching bag
    with dissolve

    pause 0.75

    scene v12lnw24 # FPP Show Lindsey looking at punching bag, mouth open
    with dissolve

    li "Okay, so I'm sure he looked away from the person and then kicked back. Like this."

    scene v12lnw25 # FPP Show Lindsey turning her back on the punching bag
    with dissolve

    pause 0.5

    play sound sound.hit
    scene v12lnw25a # FPP Same angle as v12lnw25, Lindsey kicking backward at the punching bag while looking away
    with dissolve

    pause 1.25

    scene v12lnw24a # FPP Same angle as v12lnw24, Lindsey looking at MC, mouth closed
    with dissolve

    u "What was that, a donkey kick?"

    scene v12lnw24
    with dissolve

    li "I actually think it's called steed kick, you know, like speed kick but steed."

    scene v12lnw24a
    with dissolve

    u "Haha, yeah I get it."

    scene v12lnw24
    with dissolve

    li "Try it."

    scene v12lnw24a
    with dissolve

    u "Okay."

    scene v12lnw25b # TPP Same angle as v12lnw25, MC turning his back on the punching bag like Lindsey did
    with dissolve

    menu:
        "Hard":
            $ reputation.add_point(RepComponent.TROUBLEMAKER)
            play sound sound.hit
            scene v12lnw25c # TPP Same angle as v12lnw25, MC kicking back at the punching bag, missing, and losing his balance
            with dissolve

            pause 1.25

            scene v12lnw24b # FPP Same angle as v12lnw24, Lindsey smiling with mouth closed
            with dissolve

            u "That was embarrassing."

            scene v12lnw24c # FPP Same angle as v12lnw24, Lindsey smiling with mouth open
            with dissolve

            li "Just a little. *Chuckles*"
            
        "Steady":
            play sound sound.hit
            scene v12lnw25d # TPP Same angle as v12lnw25, MC kicking back at the punching bag, the kick landing squarly and knocking the bag back
            with dissolve

            pause 1.25

            scene v12lnw24b
            with dissolve

            u "Hell yeah!"

            scene v12lnw24c
            with dissolve

            li "Your kick just made mine look bad."

            scene v12lnw24b
            with dissolve

            u "What can I say? I'm a kung fu master. *Chuckles*"

    scene v12lnw24d # FPP Same angle as v12lnw24, Lindsey looking away, smiling with mouth open
    with dissolve

    li "Let's get out of here."

    scene v12lnw24b
    with dissolve

    u "Haha, alright."

    scene v12lnw26 # TPP Show Lindsey and MC exiting the gym into the dark night
    with dissolve

    pause 0.75

    scene v12lnw27 # TPP Show Lindsey and MC entering the hotel front entrance into the lobby
    with dissolve

    pause 0.75

    stop music fadeout 3
    play music music.v12_Track_Scene_29_6 fadein 2

    scene v12lnw28 # FPP Show Lindsey, back at hotel lobby, Lindsey smiling with mouth closed
    with dissolve

    u "So, did you have fun?"

    scene v12lnw28a # FPP Same angle as v12lnw28, Lindsey smiling with mouth open
    with dissolve

    li "I did, definitely gonna be going to an actual gym a lot more. Thanks for going with me."

    scene v12lnw28
    with dissolve

    u "Anytime."

    scene v12lnw28a
    with dissolve

    li "Have a good night."

    scene v12lnw28
    with dissolve

    u "You too, Linds."

    scene v12lnw28b # FPP Same angle as v12lnw28, Lindsey with her eyebrow raised and a curious smile, mouth open
    with dissolve

    li "Woah, been a while since you called me that."

    scene v12lnw28
    with dissolve

    u "*Chuckles* Thought I'd bring it back."

    scene v12lnw28a
    with dissolve

    li "Haha, okay."

    scene v12lnw29 # FPP Show Lindsey walking away toward her hotel room
    with dissolve

    pause 0.75

    stop music fadeout 3

    jump v12_room_sneak #scene 30