# SCENE 16: The story of Valentine
# Locations: Church, Hotel Lobby, Sidewalk
# Characters: MC (Outfit: 5), RILEY (Outfit: 2), MR. LEE (Outfit: 1), LINDSEY (Outfit: 1), IMRE (Outfit: 1), BISHOP (Outfit: 1), NORA (Outfit: 1), CHARLI (Outfit: 1), CHRIS (Outfit: 1), CHLOE (Outfit: 5)
# Time: Afternoon
# Phone Images: None

label v12_valentine:
    scene v12val1 # FPP. MC and Riley in the lobby, looking at each other, Riley slight smile, mouth closed
    with dissolve

    u "Home sweet home... *Laughs* Feeling alright?"

    play music "music/v12/Track Scene 16_1.mp3" fadein 2

    scene v12val1a # FPP. Same as v12val1, Riley slight smile, mouth open
    with dissolve

    ri "Yeah, thanks. I'm feeling good, actually. Two clues down, next stop Amsterdam."

    scene v12val1
    with dissolve

    u "Hell yeah."

    scene v12val2 # TPP. Show Mr. Lee walking towards Riley and MC, Mr. Lee slightly worried, mouth closed, Riley and MC looking at each other, both slightly smiling, mouths closed
    with dissolve

    pause 0.75

    scene v12val3 # FPP. Same positioning as v12val1, Mr. Lee now next to MC and Riley, MC looking at Mr. Lee, Mr. Lee looking at Riley, Mr. Lee slight smile, mouth open (Only Mr. Lee in shot)
    with dissolve

    lee "Riley, did something hold you two back? I thought you'd be returning sooner."

    scene v12val1b # FPP. Same as v12val1, Riley looking at Mr. Lee, Riley slight smile, mouth open
    with dissolve

    ri "Sorry, we were doing the second riddle."

    scene v12val3
    with dissolve

    lee "Oh, no need to apologize for that. I'm happy to see you guys being so productive with the hunt!"

    scene v12val1b
    with dissolve

    ri "Of course. By the way, I hate to miss out on the next activity but I twisted my ankle a bit while we were hunting, and I was hoping to rest it off a little..."

    scene v12val3
    with dissolve

    lee "Of course, yes! Ms. Rose will be staying here at the hotel so if you need anyone at all, you go to her."

    scene v12val1b
    with dissolve

    ri "Understood, thank you."

    scene v12val1c # FPP. Same as v12val1, Riley walking away, slight smile, mouth closed
    with dissolve

    pause 0.75

    scene v12val3a # FPP. Same as v12val3, Mr. Lee looking at MC, Mr. Lee slight smile, mouth open (Riley has already walked away)
    with dissolve

    lee "Alright, so... Students, today we are going to learn a bit about love."

    scene v12val4 # FPP. Same positioning as v12val3a, Lindsey now in the lobby, Nora standing next to her. MC looking at Lindsey, Lindsey looking at Mr. Lee (Only Lindsey in shot, show Nora only slightly, standing next to her), Lindsey slight smile, mouth open
    with dissolve

    li "Oooo, okay. *Chuckles*"

    scene v12val3a
    with dissolve

    lee "I notice everyone isn't here, but that's okay. I was secretly hoping we'd have a smaller turn out for this one just so those that came could enjoy it all the better."

    scene v12val4
    with dissolve

    li "What exactly are we doing?"

    scene v12val3b # FPP. Same as v12val3a, Mr. Lee looking at Lindsey, Mr. Lee slight smile, mouth open
    with dissolve

    lee "Well... As I said, we're learning about love today. How could you go to the City of Love without doing so?"

    scene v12val3a
    with dissolve

    lee "There's a church nearby and the bishop has been gracious enough to teach you students the story of Valentine. I could teach you myself, but I'm sure this will be a much more enjoyable experience."

    scene v12val4
    with dissolve

    li "This is kind of exciting... *Chuckles*"

    scene v12val3b
    with dissolve

    lee "Indeed it is. As a matter of fact, the church we're going to was St. Valentine's primary church when he left Italy. Cool, right?"

    scene v12val3a
    with dissolve

    lee "So, if everyone's ready. You guys know the drill, shuttles are outside."

    scene v12val5 # TPP. Show MC, Lindsey, Chris leaving the hotel lobby, all slightly smiling, mouths closed, Chris on his phone
    with dissolve

    pause 0.75

    scene v12val6 # TPP. Show MC, Imre and Charli boarding the shuttle, MC and Imre slightly smiling, mouths closed, Charli behind them, rolling his eyes, slightly annoyed, mouth closed
    with dissolve

    pause 0.75

    scene v12val7 # TPP. Show shuttle on road
    with dissolve

    pause 0.75

    scene v12val8 # TPP. Show MC, Chris, Nora unboarding the shuttle, they're in front of the church now, Nora slightly sad, mouth closed, Chris slight smile, mouth closed, on his phone, MC slight smile, mouth closed
    with dissolve

    pause 0.75

    scene v12val9 # FPP. Mr. Lee standing in front of the church, Imre and MC in front of Mr. Lee. MC looking at Mr. Lee, Mr. Lee looking at MC, Mr. Lee slightly serious expression, mouth open (Only Mr. Lee in shot)
    with dissolve

    lee "Everyone please be on your best behavior, I am reminding you that this is a church."

    scene v12val10 # FPP. Same positioning as v12val9, MC looking at Imre, Imre looking at Mr. Lee, Imre smiling, mouth open (Only Imre in shot)
    with dissolve

    imre "Ha, right. No promises."

    scene v12val9a # FPP. Same as v12val9, Mr. Lee looking at Imre, Mr. Lee angry, mouth closed
    with dissolve

    pause 1.25

    scene v12val10a # FPP. Same as v12val10, Imre worried, mouth open
    with dissolve

    imre "I'm just kidding... Sheesh."

    scene v12val11 # TPP. Show MC and Mr. Lee walking into the church, both slightly smiling, mouths closed, the bishop walking towards them (Bishop is around 50 years old), smiling, mouth closed
    with dissolve

    pause 1.25

    stop music fadeout 3
    play music "music/v12/Track Scene 16_2.mp3" fadein 2

    scene v12val12 # FPP. MC, Mr. Lee and Nora standing next to each other, in front of them is the bishop, MC looking at bishop, bishop looking at Mr. lee, bishop smiling, mouth open (Only Bishop in shot)
    with dissolve

    bishop "Mr. Lee! These must be your students, it's wonderful to meet you all. Has your teacher informed you of what you'll be learning today?"

    scene v12val13 # FPP. Same positioning as v12val12, MC looking at Nora, Nora looking at the bishop, Nora slight smile, mouth open (Only Nora in shot)
    with dissolve

    no "He did."

    scene v12val12a # FPP. Same as v12val12, bishop looking at Nora, bishop slight smile, mouth open
    with dissolve

    bishop "Very good. Please follow me and take a seat up here at the front."

    scene v12val14 # TPP. Show MC midway through sitting down at the front, Lindsey sitting down next to him, Chloe next to Lindsey, Imre next to MC, Charli next to Imre, Mr. Lee, Chris and Nora on the other bench. (Show only MC and Lindsey), both slightly smiling, mouths closed
    with dissolve

    pause 1

    scene v12val15 # FPP. Same positioning as v12val14, bishop standing at the altar, MC looking at the bishop, bishop looking at MC, bishop slight smile, mouth open (Only bishop in shot)
    with dissolve

    bishop "First, I'll introduce myself. My name is Bishop Jenkins. I've been a member of this church for about thirty years now and I've had the pleasure of telling the story of Valentine for decades."
    bishop "Are any of you familiar with the story?"

    scene v12val16 # FPP. Same positioning as v12val14, MC looking at Nora, Nora looking at bishop, Nora slight smile, mouth open (Only Nora in shot)
    with dissolve

    no "I'm familiar with the holiday. *Chuckles*"

    scene v12val17 # FPP. Same positioning as v12val14, MC looking at Lindsey, Lindsey looking at bishop, Lindsey slight smile, mouth open (Only Lindsey in shot)
    with dissolve

    li "*Laughs* Yeah, that's about it for me as well."

    scene v12val15
    with dissolve

    bishop "*Chuckles* That's usually the case for most students. Due to most holidays stemming from religious roots, it's rare for schools to actually teach students the meaning behind Valentine's."
    bishop "But today I'll be doing just that. Have any of you heard of St. Valentine?"

    scene v12val18 # FPP. Same positioning as v12val14, MC looking at Imre, Imre looking at bishop, Imre slight smile, mouth open (Only Imre in shot)
    with dissolve

    imre "That's the flying baby with the bow and arrow that shoots people, right?"

    scene v12val19 # FPP. Same positioning as v12val14, MC looking at Mr. Lee, Mr. Lee looking at Imre, Mr. Lee slightly annoyed, mouth open (Only Mr. Lee in shot)
    with dissolve

    lee "Imre, behave yourself. This is no time for jokes."

    scene v12val18a # FPP. Same as v12val18, Imre looking at Mr. Lee, Imre slightly annoyed, mouth open
    with dissolve

    imre "I wasn't trying to be funny..."

    scene v12val15a # FPP. Same as v12val15, bishop looking at Mr. Lee, bishop slight smile, mouth open
    with dissolve

    bishop "There are no silly questions, no bad guesses. We welcome all, Mr. Lee."

    scene v12val15b # FPP. Same as v12val15, bishop looking at Imre, bishop slight smile, mouth open
    with dissolve

    bishop "To you young man, who you're thinking of is the fictional character named Cupid commonly associated with Valentine's day due to being the Roman god of love."

    scene v12val19a # FPP. Same as v12val19, Mr. Lee looking at bishop, Mr. Lee slight smile, mouth open
    with dissolve

    lee "Greek."

    scene v12val15a
    with dissolve

    bishop "I'm sorry?"

    scene v12val19a
    with dissolve

    lee "The Greek God of love."

    scene v12val15a
    with dissolve

    bishop "Another common mistake. Cupid is the Roman God of Love and within Greek mythology, he is the son of Aphrodite who is the God of Love in Greek mythology."

    scene v12val17a # FPP. Same as v12val17, Lindsey looking at MC, Lindsey slight smile, mouth closed
    with dissolve

    u "*Whisper* I'm sensing some tension between Mr. Lee and Jenkins."

    scene v12val17b # FPP. Same as v12val17a, Lindsey slight smile, mouth open
    with dissolve

    li "*Whisper* Yeah, me too."

    scene v12val15
    with dissolve

    bishop "Two thousand years ago, the Romans had the world's strongest army. They were so strong, they conquered nearly all of Europe, parts of Asia and Africa."

    scene v12val20 # FPP. Same positioning as v12val14, MC looking at Charli, Charli looking at bishop, Charli slight smile, mouth open (Only Charli in shot)
    with dissolve

    charli "Around the time Jesus was killed?"

    scene v12val15c # FPP. Same as v12val15, bishop looking at Charli, bishop slight smile, mouth open
    with dissolve

    bishop "Correct. After some time, they began to grow very rich and with that wealth came laziness. Life in the army was harsh, so many soldiers longed for the comfort of home... And most importantly, marriage."

    scene v12val17
    with dissolve

    li "Awww."

    scene v12val15
    with dissolve

    bishop "Sadly, the Emperor at the time grew worried that his army was growing weak and decided to ban soldiers from getting married."

    scene v12val16a # FPP. Same as v12val16, Nora shocked, mouth open
    with dissolve

    no "WAIT, WHAT?!"

    scene v12val17c # FPP. Same as v12val17, Lindsey shocked, mouth open
    with dissolve

    li "How could he do that? Was there not some type of congress or anything?"

    scene v12val15d # FPP. Same as v12val15, bishop looking at Lindsey, bishop slight smile, mouth open
    with dissolve

    bishop "Times weren't as they are today and dictators ruled the world in the form of Kings, Emperors and false gods."

    scene v12val17c
    with dissolve

    li "That's not fair."

    scene v12val18
    with dissolve

    imre "And it's probably why Rome doesn't exist anymore."

    scene v12val21 # FPP. Same positioning as v12val14, MC looking at Chris, Chris looking at Imre, Chris slight smile, mouth open (Only Chris in shot)
    with dissolve

    ch "Rome still exists. *Chuckles*"

    scene v12val18b # FPP. Same as v12val18, Imre looking at Chris, Imre slightly annoyed, mouth open
    with dissolve

    imre "No it doesn't."

    scene v12val21
    with dissolve

    ch "It's the capital of Italy..."

    scene v12val18b
    with dissolve

    imre "I meant the Empire doesn't exist."

    scene v12val21
    with dissolve

    ch "Oh... *Chuckles*"

    scene v12val18b
    with dissolve

    imre "Jeez guys, I'm not stupid."

    scene v12val15b
    with dissolve

    bishop "Ahh, to be young. So, during that time many Romans were Christians and there was a Bishop leader by the name of Bishop Valentine."

    scene v12val16
    with dissolve

    no "You hold the same position that Valentine had."

    scene v12val15e # FPP. Same as v12val15, bishop looking at Nora, bishop slight smile, mouth open
    with dissolve

    bishop "Exactly. And like Valentine, I too believe that if a man and a woman love each other, they should both get married."
    bishop "Valentine went against the Emperor's law and allowed soldiers to get married at his church in secret."

    scene v12val19a
    with dissolve

    lee "I'm pretty sure he actually did the ceremonies during the night, deep within the forest to not avoid suspicion."

    scene v12val15a
    with dissolve

    bishop "And, do you have a source for that information?"

    scene v12val19a
    with dissolve

    lee "I wanted to be just as informed on the topic as you, so I did a barrage of research during my time here in Europe."

    scene v12val15a
    with dissolve

    bishop "No disrespect intended, but I doubt your days of research can make up for decades of studying ancient text."

    scene v12val19a
    with dissolve

    lee "I-"

    scene v12val15
    with dissolve

    bishop "Anyway... Eventually word got out and the Emperor not only demanded Valentine to stop his secret weddings, but also wanted him to convert to the Roman faith."
    bishop "When Valentine refused, he was sentenced to death."

    scene v12val18
    with dissolve

    imre "Bro... What kind of story is this? Where is the love?"

    scene v12val15b
    with dissolve

    bishop "Bear with me, my son."
    bishop "While in prison, prior to his execution, the jailer's blind daughter would bring him his meals and from that stemmed many conversations. From those many conversations, they fell in love."
    bishop "One day, Saint Valentine slid his hand through the bars of his cell, touched the lids of the girl's eyes and miraculously she could see."

    scene v12val20a # FPP. Same as v12val20, Charli looking down, sligtly annoyed, mouth slightly open
    with dissolve

    charli "*Whisper* Good god, this is nonsense."

    scene v12val17d # FPP. Same as v12val17, Lindsey looking at Charli, Lindsey slightly annoyed, mouth open
    with dissolve

    li "*Whisper* Charli, stop it."

    scene v12val15
    with dissolve

    bishop "Valentine's execution was set for the 14th of February, and on his last day he wrote his beloved a letter and signed it, \"from your Valentine.\""

    scene v12val16
    with dissolve

    no "That is so sweet but so sad."

    scene v12val15e
    with dissolve

    bishop "Valentine's beloved received the letter and before the execution she pleaded with her father, the jailer, to help Valentine escape."
    bishop "Her father did just that, and Valentine came to what is now modern day Paris."

    scene v12val19a
    with dissolve

    lee "He did not escape, he was executed. That's what makes the story a tragedy. None of the known research mentions that he escaped."

    scene v12val15a
    with dissolve

    bishop "Clarification, none of your known research."

    scene v12val19a
    with dissolve

    lee "I'd like to see what artifacts prove his escape."

    scene v12val15f # FPP. Same as v12val15a, bishop slightly annoyed, mouth open
    with dissolve

    bishop "Mr. Lee, we are all professionals at what we've dedicated our lives toward."
    bishop "Your knowledge of history in a general sense is something I'd never attempt to challenge. I'd appreciate the same respect for my specialization."

    scene v12val19a
    with dissolve

    lee "I just don't want my students being taught false information."

    scene v12val15f
    with dissolve

    bishop "*Sighs*"

    scene v12val18c # FPP. Same as v12val18a, Imre slight smile, mouth open
    with dissolve

    imre "Mr. Lee, \"if you want to behave like a child I'll treat you like one... remember, this is a church.\""

    scene v12val19b # FPP. Same as v12val19, Mr. Lee looking at MC, Mr. Lee slightly embarassed, mouth closed
    with dissolve

    u "*Laughs*"

    scene v12val22 # FPP. Same positioning as v12val14, MC looking at Chloe, Chloe looking at Mr. Lee, Chloe laughing (Only Chloe in shot)
    with dissolve

    cl "*Laughs*"

    scene v12val21a # FPP. Same as v12val21, Chris looking at Mr. Lee, Chris laughing
    with dissolve

    ch "*Laughs*"

    scene v12val20b # FPP. Same as v12val20, Charli looking at Mr. Lee, Charli slight smile, mouth open
    with dissolve

    charli "Even I have to admit, that was ironically funny."

    scene v12val15
    with dissolve

    bishop "To carry on, students, Valentine dedicated his life to ensuring that others could live a life with love and in the end, it was love that saved him."

    scene v12val16
    with dissolve

    no "Very beautiful."

    scene v12val15e
    with dissolve

    bishop "You know, it would be outstanding if we could demonstrate a genuine wedding ceremony. Would any of you be willing to volunteer?"

    scene v12val17
    with dissolve

    li "Oh my gosh! I love weddings!"

    scene v12val16
    with dissolve

    no "Bishop Jenkins, I'd be willing to volunteer."

    scene v12val17e # FPP. Same as v12val17, Lindsey looking at Nora, Lindsey slight smile, mouth open
    with dissolve

    li "Woohoo, go Nora."

    scene v12val16b # FPP. Same as v12val16, Nora looking at Chris, Nora slight smile, mouth open
    with dissolve

    no "Umm, Chris, will you join me?"

    scene v12val21b # FPP. Same as v12val21, Chris looking at Nora, Chris slight smile, mouth open
    with dissolve

    ch "I'd be happy to."

    scene v12val23 # TPP. Show Chris and Nora walking up to the altar, hands held, both smiling, mouths closed
    with dissolve

    pause 1

    scene v12val24 # FPP. Nora and Chris at the altar, standing in front of each other, bishop in front of them (mock wedding), bishop looking at Chris, bishop slight smile, mouth open, Chris and Nora smiling, mouths closed
    with dissolve

    bishop "Wonderful, now do either of you-"

    play sound "sounds/call.mp3"

    scene v12val24a # FPP. Same as v12val24, bishop looking at Chris, bishop startled, Chris looking at his phone, worried, mouth open, Nora looking at Chris, Nora angry, mouth closed
    with dissolve

    ch "Oh no."

    scene v12val24b # FPP. Same as v12val24a, Chris looking at Nora, Chris worried, mouth open, Nora angry, mouth closed, bishop surprised, mouth closed
    with dissolve

    ch "I've been waiting on this call, I'm so sorry. I have to take this."

    stop sound

    scene v12val24c # FPP. Same as v12val24, Chris no longer at the altar, Nora looking at the church exit, Nora shocked, mouth closed, bishop looking at Nora, bishop uncomfortable smile, mouth open
    with dissolve

    bishop "Well, that wouldn't be the first time I've seen someone left at the altar, but good thing this isn't a real wedding, right?"

    scene v12val24d # FPP. Same as v12val24c, bishop looking at MC, bishop slight smile, mouth open, Nora very embarassed, mouth closed, looking down
    with dissolve

    bishop "Will another gentleman please come and volunteer?"

    scene v12val25 # TPP. Same positioning as v12las14, show Charli halfway through standing up, slight smile, mouth closed, Imre looking at Charli, Imre worried, mouth closed
    with dissolve

    pause 0.75

    scene v12val25a # TPP. Same as v12val25, Imre pushing Charli back into his seat, Charli startled, mouth closed, Imre slight smile, mouth closed
    with dissolve

    pause 0.75

    scene v12val18
    with dissolve

    imre "My buddy [name] will do it."

    scene v12val18d # FPP. Same as v12val18, Imre looking at MC, Imre slight smile, Imre mouth closed
    with dissolve

    u "Oh, yeah. Sure."

    scene v12val26 # TPP. Show MC standing up in front of where he was seated, looking at Charli, MC smirking, mouth closed, Charli annoyed, mouth closed
    with dissolve

    pause 1

    scene v12val23a # TPP. Same as v12val23, but MC is walking towards the altar (No Nora or Chris), MC slight smile, mouth closed
    with dissolve

    pause 1

    scene v12val27 # FPP. MC and Nora standing in front of each other at the altar, Bishop in front of both of them (mock wedding), other characters same positioning as v12val14, MC looking at bishop, bishop looking at MC, bishop slight smile, mouth open (only bishop in shot)
    with dissolve

    bishop "Now, there is a set of default vows designed by the church, but participants are always welcome to say their own vows."

    scene v12val28 # FPP. Same positioning as v12val27, MC looking at Mr. Leem Mr. Lee looking at bishop, Mr. Lee slight smile, mouth open (Only Mr. Lee in shot)
    with dissolve

    lee "I'd like to see some originality, so the students should do their own."

    scene v12val27a # FPP. Same as v12val27, Bishop looking at Mr. Lee, Bishop slight smile, mouth open
    with dissolve

    bishop "So be it. We've already rushed the procedures of the ceremony quite a bit, but I'm betting most of you have seen a wedding so I'll assume you get the gist."

    scene v12val29 # FPP. Same positioning as v12val27, MC looking at Lindsey, Lindsey looking at bishop, Lindsey slight smile, mouth open (Only Lindsey in shot)
    with dissolve

    li "I sure do."

    scene v12val30 # FPP. Same positioning as v12val27, MC looking at Nora, Nora looking down, embarassed, mouth slightly open (Only Nora in shot)
    with dissolve

    no "*Whisper* This is so embarrassing."

    scene v12val30a # FPP. Same as v12val30, Nora embarassed, mouth closed
    with dissolve

    menu:
        "Leave her be":
            $ add_point(KCT.BRO)
            u "(Not the time, not in front of everyone.)"

            scene v12val29a # FPP. Same as v12val29, Lindsey looking at Nora, Lindsey smiling, excited, mouth open (screaming)
            with dissolve

            li "YOU GOT THIS NORA!"

            scene v12val30b # FPP. Same as v12val30, Nora looking at Lindsey, Nora slight smile, mouth closed
            with dissolve

            pause 0.75

        "Comfort her":
            pause 0.5
            $ add_point(KCT.BOYFRIEND)
            scene v12val31 # TPP. Show MC leaning in a whispering in Nora's ear, holding her hand, Nora slight smile, mouth closed, MC slight smile, mouth open
            with dissolve

            u "*Whisper* I know that bothered you, but try and roll it off your shoulder, yeah? Worry about what's in front of you, not what's down the hall."

    stop music fadeout 3
    play music "music/v12/Track Scene 16_3.mp3" fadein 2

    scene v12val30c # FPP. Same as v12val30, Nora looking at MC, Nora slight smile, mouth closed
    with dissolve

    pause 1.5

    scene v12val27b # FPP. Same as v12val27, bishop looking at Nora, bishop slight smile, mouth open
    with dissolve

    bishop "What's your name, miss?"

    scene v12val30d # FPP. Same as v12val30, Nora looking at bishop, Nora slight smile, mouth open
    with dissolve

    no "Nora?"

    scene v12val27
    with dissolve

    bishop "And you, sir?"

    scene v12val27c # FPP. Same as v12val27, bishop slight smile, mouth closed
    with dissolve

    u "[name]."

    scene v12val27b
    with dissolve

    bishop "Very good, ladies first. Miss Nora, just speak from the heart. Don't think too hard."

    scene v12val30e # FPP. Same as v12val30c, Nora slight smile, mouth open
    with dissolve

    no "*Sighs* Okay. [name], when I think about the type of person I want to marry, I think of someone who is patient. Someone who is selfless. Someone who is always there for me, even when I'm least expecting it."

    scene v12val30f # FPP. Same as v12val30e, different pose
    with dissolve

    no "You've been all of those things, and more. And still, to this day, I don't know why you do the things you do for me, but I'm so appreciative. You're like a dream..."

    if nora.relationship >= Relationship.LIKES:
        scene v12val30g # FPP. Same as v12val30c, Nora eyes tearing up (not crying, just tearing up), slight smile, mouth closed
        with dissolve

        pause 1.5
    
    scene v12val30e
    with dissolve

    no "And standing here now, I feel like I'm exactly where I'm supposed to be... I uh, I love you. *Chuckles*"

    if chloe.relationship >= Relationship.GIRLFRIEND:
        scene v12val32 # FPP. Same positioning as v12val27, MC looking at Chloe, Chloe looking at Lindsey, Chloe slight smile, mouth open (Only Chloe in shot)
        with dissolve

        cl "*Whispers* Wow, she couldn't have saved all that for her own boyfriend? *Chuckles*"

        scene v12val29b # FPP. Same as v12val29, Lindsey looking at Chloe, Lindsey slight smile, mouth open
        with dissolve

        li "*Whispers* Wait, you and [name] are dating?"

        scene v12val32
        with dissolve

        cl "*Whispers* Oh! Yeah, haha. He asked me out while we were in London... It was so cute."

        scene v12val29b
        with dissolve

        li "*Whispers* Oh my gosh! That's so sweet, Chloe! I had no idea..."

    scene v12val27b
    with dissolve

    bishop "That was very, very beautifully spoken."

    scene v12val27
    with dissolve

    bishop "And now. Your vows, sir."

    scene v12val30c
    with dissolve

    menu:
        "Joke":
            $ add_point(KCT.BRO)
            scene v12val30h # FPP. Same as v12val30c, different pose
            with dissolve

            u "Nora, I remember living alone on the streets with nothing but the clothes on my back."
            u "The day you came up to me, I thought I'd get a couple dollars for a chicken sandwich, but I ended up with the best digits I could have ever hoped for - the number to an opportunity."

            scene v12val30g
            with dissolve

            u "I've been working for a few days now, and I don't get my first check until I've worked for two weeks but... I wanted to pay you back for helping me, so here I am. Swearing myself to you for eternity."

            scene v12val30e
            with dissolve

            no "So kind of you. *Chuckles*"

            scene v12val33 # FPP. Same positioning as v12val27, MC looking at Imre, Imre looking at MC, Imre laughing (Only Imre in shot)
            with dissolve

            imre "*Laughs*"

            scene v12val34 # FPP. Same positioning as v12val27, MC looking at Charli, Charli looking at MC, Charli slightly annoyed, mouth open (Only Charli in shot)
            with dissolve

            charli "Can't even be serious for two seconds..."

            scene v12val27
            with dissolve

            bishop "Haven't heard any vows like that before, but... Vows are vows I suppose."

        "Be serious":
            $ add_point(KCT.BOYFRIEND)
            scene v12val30h
            with dissolve

            u "Nora, I haven't always made the best choices in life, or had the best goals in life... But, when I met you, I started to see things a little differently."

            scene v12val30c
            with dissolve
            
            u "I never saw myself settling down or living the straight and narrow life, but at this point I can't imagine what my life would be like without you... You've become the brightest light in my world. And, I love you."

            if chloe.relationship >= Relationship.GIRLFRIEND:
                scene v12val32a # FPP. Same as v12val32, Chloe looking at MC, Chloe slight smile, mouth open
                with dissolve

                cl "Sounds like you actually mean that, [name]. *Laughs*"

                scene v12val32b # FPP. Same as v12val32a, Chloe slight smile, mouth closed
                with dissolve

                u "Imagine it was you I was talking to."

                scene v12val29b
                with dissolve

                li "*Whisper* What was that about?"

                scene v12val32
                with dissolve

                cl "*Whisper* Just making sure he knows that I'm the only girl he should be talking to like that. *Chuckles*"

            scene v12val27
            with dissolve

            bishop "Very beautiful vows, my son."

    scene v12val27
    with dissolve

    bishop "Now, this is normally the part where our beautiful couple would kiss, solidifying their union."

    scene v12val28
    with dissolve

    lee "Actually, you forgot to ask if anyone had any objections."

    scene v12val27a
    with dissolve

    bishop "I also forgot to mention that any interruptions were worthy of removal from the ceremony."

    scene v12val27
    with dissolve

    bishop "As I was saying, this is normally the moment of the kiss."

    if chloe.relationship >= Relationship.GIRLFRIEND:
        scene v12val30i
        with dissolve

        pause 1.5

        scene v12val27 
        with dissolve

        bishop "Very sweet, you two."

    else:
        scene v12val30c
        with dissolve

        menu:
            "Fist bump":
                $ add_point(KCT.BRO)
                scene v12val30i # FPP. Same as v12val30c, MC has his fist extended, Nora slight smile, mouth closed, fist bumping MC
                with dissolve

                pause 1.25

                scene v12val27
                with dissolve

                bishop "Very sweet, you two."

            "Kiss her cheek":
                $ add_point(KCT.BOYFRIEND)
                scene v12val35 # TPP. Show MC kissing Nora's cheek, Nora slight smile, blushing, mouth closed
                with dissolve

                play sound "sounds/kiss.mp3"

                pause 1.25

                scene v12val27
                with dissolve

                $ grant_achievement("you_may_kiss_the_bride")
                bishop "That was very sweet of you, young man."
        
    scene v12val27a
    with dissolve

    bishop "Alright everyone, I hope you enjoyed the presentation. That is all I had prepared for you, so if your teacher has no more corrections for me, you are free to leave."

    scene v12val28
    with dissolve

    lee "I do not."

    scene v12val27a
    with dissolve

    bishop "Very well, I enjoyed you all very much. Do come back!"

    scene v12val29c # FPP. Same as v12val29a, Lindsey slight smile, mouth open (Not screaming)
    with dissolve

    li "Wait, where's my bouquet? *Chuckles*"

    scene v12val30j # FPP. Same as v12val30, Nora fake tossing a bouquet at Lindsey's direction, Nora slight smile, mouth open
    with dissolve

    no "There you go."

    scene v12val29c
    with dissolve

    li "Yay! Thanks. *Chuckles*"

    scene v12val28
    with dissolve

    lee "Alright everyone, let's get prepared to shuttle back."

    scene v12val36 # TPP. Show Mr. Lee walking away from the bishop, bishop waving goodbye, Mr. Lee's back to the bishop, bishop slight smile, mouth closed, Mr. Lee annoyed, mouth slightly open
    with dissolve

    lee "*Whisper* Stuck up bishop."

    scene v12val37 # TPP. Show Lindsey in a corner, Imre, MC and Charli moving towards the exit. All slight smiles, mouth closed
    with dissolve

    pause 0.75

    scene v12val37a # TPP. Same as v12val37, Lindsey pulling MC to the corner, Imre and Charli follow behind, all slight smiles, mouths closed
    with dissolve

    pause 0.75

    scene v12val38 # FPP. MC, Imre and Charli standing next to each other, Lindsey in front of them. MC looking at Lindsey, Lindsey looking at MC, Lindsey slight smile, mouth open
    with dissolve

    li "Hey guys, we're not going back to the hotel just yet."

    scene v12val38a # FPP. Same as v12val38, Lindsey slight smile, mouth closed
    with dissolve

    u "Where are we going?"

    scene v12val38
    with dissolve

    li "We're going to an escape room."

    scene v12val39 # FPP. Same positioning as v12val38, MC looking at Imre, Imre looking at Lindsey, Imre excited, mouth open
    with dissolve

    imre "Woah, really?!"

    scene v12val38b # FPP. Same as v12val38, Lindsey looking at Imre, Lindsey slight smile, mouth open
    with dissolve

    li "Yes, really! We're going to meet Aubrey over there right now."

    scene v12val38a
    with dissolve

    u "Alright, cool. Sounds good to me."

    scene v12val38c # FPP. Same as v12val38, Lindsey looking at Charli, Lindsey slight smile, mouth open
    with dissolve

    li "Charli?"

    scene v12val40 # FPP. Same positioning as v12val38, MC looking at Charli, Charli looking at Lindsey, Charli slight smile, mouth open
    with dissolve

    charli "Sure."

    scene v12val38
    with dissolve

    li "Alright good. It's right around the corner from here, so we can walk."

    scene v12val41 # TPP. Show MC, Lindsey, Imre and Charli walking out of the church, all slightly smiling, mouths closed
    with dissolve

    pause 0.75

    stop music fadeout 3
    play music "music/v12/Track Scene 16_4.mp3" fadein 2

    scene v12val42 # TPP. Show Charli walking next to MC, Imre and Lindsey walking in front of them, on sidewalk, all slightly smiling, mouths closed
    with dissolve

    pause 0.75

    if chloe.relationship >= Relationship.GIRLFRIEND and lauren.relationship < Relationship.GIRLFRIEND:
        scene v12val43 # FPP. Charli next to MC on the sidewalk, not walking, Charli and MC looking at each other, Charli slightly annoyed, mouth open
        with dissolve

        charli "So, you're dating Chloe?"

        scene v12val43a # FPP. Same as v12val43, Charli slightly annoyed, mouth closed
        with dissolve

        u "I am."

        scene v12val43
        with dissolve

        charli "Interesting."

        scene v12val44 # FPP. MC watches as Charli walks off, back turned to MC
        with dissolve

        u "(Fucking weirdo.)"

    elif chloe.relationship >= Relationship.GIRLFRIEND:
        scene v12val43
        with dissolve

        charli "So, you're dating Chloe?"

        scene v12val43a
        with dissolve

        u "I am."

        scene v12val43
        with dissolve

        charli "And Lauren?"

        scene v12val43a
        with dissolve

        u "Look man, mind your own fucking business. Alright?"

        scene v12val43
        with dissolve

        charli "Hmm... You made a big mistake, [name]."

        scene v12val44
        with dissolve

        u "(Fucking dickhead.)"

    scene v12val45 # TPP. Show MC, Imre and Lindsey arriving at the escape room, Aubrey in the background, all smiling, mouths closed
    with dissolve

    pause 0.75

    stop music fadeout 3

    jump v12_escape_room #scene 17