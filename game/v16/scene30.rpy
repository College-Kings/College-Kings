# SCENE 30: Wolves 1
# Locations: Wolves house
# Characters: MC (Outfit: 9), CHRIS (Outfit: 2), SEBASTIAN (Outfit: 1), IMRE (Outfit: 2)
# Time: Afternoon

label v16s30:
    scene v16s30_1 # TPP. MC walking into the Wolves house, slight smile, mouth closed
    with fade

    pause 0.75

    scene v16s30_2 # TPP. MC walking into the living room, slightly confused, mouth closed, Chris, Imre and Sebastian are sitting in the living room, (Chris and Seb sitting enxt to each other, Imre on the couch alone, MC will sit next to him in another image) Chris mouth open, looking at Imre, slight smile, Sebastian and Imre looking at Chris, mouth closed, slight smiles
    with dissolve

    u "(Is there a meeting?)"

    scene v16s30_3 # FPP. MC standing in the living room, looking at Chris, Chris looking at MC, Chris slight smile, mouth closed
    with dissolve

    u "Am I late for something? *Laughs*"

    scene v16s30_3a # FPP. Same as v16s30_3, Chris slight smile, mouth open
    with dissolve

    ch "Nah, not officially. There's a power outage, so we're just chatting."

    scene v16s30_4 # FPP. Same positioning at v16s30_3, MC looking at Sebastian, Sebastian looking at MC, Sebastian slight smile, mouth open
    with dissolve

    se "Until we can go back online and ignore each other again, haha!"

    scene v16s30_5 # TPP. Show MC taking a seat next to Imre, both slight smiles, Imre mouth closed, MC mouth open
    with dissolve

    u "Oh, yeah. Aubrey mentioned something about power outages lately. How long's it been out for?"

    scene v16s30_6 # FPP. Same positioning as v16s30_5, MC looking at Imre, Imre looking at MC, Imre slight smile, mouth open
    with dissolve

    imre "Who knows? Our phones are dead so we don't know what time it is."

    scene v16s30_7 # FPP. MC looking at Chris, Chris looking at Imre, Chris slight smile, mouth open
    with dissolve

    ch "Imre, we have a clock in the kitchen."

    scene v16s30_6a # FPP. Same as v16s30_6, Imre looking at Chris, Imre mouth open, slight smile
    with dissolve

    imre "Dude, that thing is ancient."

    scene v16s30_7a # FPP. Same as v16s30_7, Chris looking at MC, Chris neutral expression, mouth open
    with dissolve

    ch "*Sighs* It's been about... an hour?"

    scene v16s30_8 # FPP. MC looking at Sebastian, Sebastian looking at MC, Sebastian neutral expression, mouth open
    with dissolve

    se "Yeah, something like that."

    scene v16s30_7a
    with dissolve

    ch "But who cares? It's good to have some time like this."

    scene v16s30_8a # FPP. Same as v16s30_8, Sebastian neutral, mouth closed
    with dissolve

    u "What were you chatting about?"

    scene v16s30_8
    with dissolve

    se "We were kind of talking with Chris about the Nora situation."

    scene v16s30_8a
    with dissolve

    u "(Chris talking about Nora? No surprises there...)"

    scene v16s30_7a
    with dissolve

    ch "Yeah, so back to what I was saying..."

    scene v16s30_7b # FPP. Same as v16s30_7, Chris looking at Sebastian, Chris mouth open, neutral expression
    with dissolve

    ch "I guess deep down I always knew Nora wasn't the right girl for me."

    scene v16s30_8b # FPP. Same as v16s30_8, Sebastian looking at Chris, Sebastian mouth open, neutral
    with dissolve

    se "What makes you say that?"

    scene v16s30_7b
    with dissolve

    ch "Well..."

    scene v16s30_7a
    with dissolve

    ch "I'm with my Wolves, so I know I can speak freely. Without judgement."

    scene v16s30_6a
    with dissolve

    imre "Of course. That's what we stand for."

    scene v16s30_7a
    with dissolve

    ch "I want to tell you guys something, because it'd be good to get it off of my chest. But don't get pissed off, ha."

    scene v16s30_8c # FPP. Same as v16s30_8b, Sebastian slight smile, mouth open
    with dissolve

    se "Ha! Spit it out, man. That's impossible."

    scene v16s30_6a
    with dissolve

    imre "Don't keep us in suspense any longer, bro. Come on!"

    scene v16s30_7a
    with dissolve

    ch "Okay, well. This isn't easy, but..."

    scene v16s30_7b
    with dissolve

    ch "About a year ago, during break..."

    scene v16s30_7a
    with dissolve

    ch "I hooked up with this random girl."

    scene v16s30_7c # FPP. Same as v16s30_7a, Chris mouth closed, neutral expression
    with dissolve

    u "(Oh, shit...)"

    scene v16s30_6b # FPP. Same as v16s30_6a, Imre looking very shocked, mouth open
    with dissolve

    imre "You what?"

    scene v16s30_7d # FPP. Same as v16s30_7, Chris slightly annoyed, mouth open
    with dissolve

    ch "I know it sounds bad, Imre, but just let me-"

    scene v16s30_6c # FPP. Same as v16s30_6b, Imre very angry, mouth open
    with dissolve

    imre "Let you what?! Tell me the whole goddamn story about how you managed to cheat on Nora?"

    imre "Are you for real?"

    scene v16s30_7d
    with dissolve

    ch "Imre, chill. It's not like that, bro."

    scene v16s30_8d # FPP. Same as v16s30_8c, Sebastian shocked, mouth open from shock (not talking)
    with dissolve

    pause 0.75

    scene v16s30_6c
    with dissolve

    imre "*Scoffs* Don't call me your fucking, bro."

    scene v16s30_6d # FPP. Same as v16s30_6c, Imre mouth closed, very angry
    with dissolve

    u "Imre-"

    scene v16s30_6e # FPP. Same as v16s30_6c, Imre pointing his finger at Chris, Imre evry angry, mouth open
    with dissolve

    imre "I've always looked up to you, Chris."

    scene v16s30_6c
    with dissolve

    imre "Since the day I arrived at SVC, you've talked about nothing but loyalty and brotherhood."

    scene v16s30_7d
    with dissolve

    ch "Imre, this doesn't change anything. I'm still the same person."

    scene v16s30_6c
    with dissolve

    imre "Ha. That's where you're wrong. This changes everything."

    scene v16s30_7d
    with dissolve

    ch "It was just one night, and it was a mistake."

    scene v16s30_6e
    with dissolve

    imre "Bullshit! You don't fuck someone by mistake. What happened to loyalty being everything, huh?"

    scene v16s30_9 # TPP. Imre stands up, stands close to Chris, Chris still sitting down, Imre very angry, mouth open, Chris angry, mouth closed
    with dissolve

    imre "What happened to loyalty?! Because right now, all I can think about is how lucky she is to have left you already, you fucking prick!"

    scene v16s30_9a # TPP. Same as v16s30_9, both mouths closed, both very angry
    with dissolve

    u "(I don't think I've ever seen Imre this angry before.)"

    scene v16s30_9b # TPP. Same as v16s30_9, both standing up now, Chris very angry, mouth open, Imre very angry, mouth closed
    with dissolve

    ch "Don't fucking talk to me like that, Imre. I'm still your president, and until that changes you need to show me some respect!"

    scene v16s30_9c # TPP. Same as v16s30_9b, both very angry, Chris mouth closed, Imre mouth open
    with dissolve

    imre "Respect? Are you-"

    scene v16s30_10 # FPP. MC sitting down, positioning same as v16s30_9c, Imre looking back at MC, Imre very angry, mouth open, pointing at Chris, Chris very angry, fists clenched, mouth closed
    with dissolve

    imre "Are you fucking hearing this guy?!"

    scene v16s30_8e # FPP. Same as v16s30_8, Sebastian looking up at Imre (v16s30_9c positioning), Sebastian, worried, mouth open
    with dissolve

    se "Imre, breathe for a second."

    scene v16s30_9c
    with dissolve

    imre "We all stood by and listened to you cry and complain about Nora. You even suggested that she might have been seeing someone else."

    if v13_imre_disloyal: # MC kissed Nora and Imre caught them in v13s37 
        
        scene v16s30_9d # TPP. Same as v16s30_9c, both mouths closed, both very angry
        with dissolve

        u "(Please Imre, don't...)"

    scene v16s30_9c
    with dissolve

    imre "I guess it takes a cheater to recognize one, huh?"

    scene v16s30_9b
    with dissolve

    ch "I told you this because you're my brothers and I trusted you. Doesn't brotherhood mean anything to you?"

    scene v16s30_9c
    with dissolve

    imre "Nothing about you means anything to me anymore, man. Whether that includes this fraternity or not. And that's fucking final."

    scene v16s30_9b
    with dissolve

    ch "Oh, come on, you're completely overreacting! Sit back down and I'll explain what happened, dude."

    scene v16s30_9c
    with dissolve

    imre "No, you're not my leader anymore. You're a cheating, lying, backstabbing dick-"

    scene v16s30_11 # TPP. Show Chris raising his fist (about to punch Imre), both very angry, mouths closed
    with dissolve

    pause 0.75

    scene v16s30_12 # TPP. Show Chris connecting the punch to Imre's face, both very angry, mouths closed
    with dissolve

    pause 0.75

    scene v16s30_13 # TPP. Show Imre falling to the ground, Chris has his fists up, both very angry, mouths closed
    with dissolve

    pause 0.75

    scene v16s30_14 # TPP. Imre on the floor, Chris standing over him, Imre mouth open, blood trickling down his nose, Imre wiping the blood off, very angry, Chris very angry, looking down at Imre, Chris mouth closed, fists still up
    with dissolve

    imre "Is this what you do when people don't listen? Ha, nice."

    scene v16s30_14a # TPP. Same as v16s30_14, Imre no more blood, mouth closed, very angry, Chris fists up, very angry, mouth open
    with dissolve

    ch "Do you need another one?!"

    scene v16s30_14b # TPP. Same as v16s30_14a, both very angry, mouths closed
    with dissolve

    menu:
        
        "Push Chris":
            scene v16s30_15 # TPP. Show MC pushing Chris back, Chris lowering his fists, slightly less angry, Sebastian checking on Imre on the floor, Imre angry, Sebastian concerned, MC angry, all mouths closed, only MC mouth open
            with dissolve

            u "*Grunts* What the hell is wrong with you?!"

            scene v16s30_16 # FPP. MC standing in front of Chris, Chris looking at MC, Chris angry, mouth closed
            with dissolve

            u "Look at what you're doing, bro."

            scene v16s30_16a # FPP. Same as v16s30_16, Chris looking shocked, mouth closed
            with dissolve

            pause 0.75

            scene v16s30_16b # FPP. Same as v16s30_16, Chris looking down at Imre, Chris sorry expression, mouth open
            with dissolve

            ch "I'm... I'm sorry, Imre..."

            scene v16s30_17 # FPP. Same positioning as v16s30_15, MC looking at Sebastian, Sebastian kneeling next to Imre, Sebastian worried, mouth open, looking at Chris
            with dissolve

            se "Seriously Chris, you just hit your own Wolf... What is going on with you?"

            scene v16s30_16b
            with dissolve

            ch "I don't know, he just kept on saying shit and I..."

            scene v16s30_16c # FPP. Same as v16s30_16b, Chris looking down, embarassed and sad, mouth open
            with dissolve

            ch "I snapped."

        "Stop Imre":
            
            scene v16s30_15a # TPP. Same as v16s30_15, instead, MC and Sebastian standing next to Imre on the floor, Chris same positiong, MC annoyed, Chris and Imre very angry, Sebastian worried, all mouths closed
            with dissolve

            pause 0.75

            scene v16s30_18 # FPP. Same positioning as v16s30_15a, MC looking at Imre, Imre looking at MC, mouth closed, very angry
            with dissolve

            u "Imre, for once in your life... Shut up."

            scene v16s30_18a # FPP. Same as v16s30_18, Imre mouth open, very angry
            with dissolve

            imre "You're seriously siding with him after what he did?!"

            scene v16s30_18
            with dissolve

            u "Seriously, just let it go before things get worse."

            scene v16s30_18a
            with dissolve

            imre "Easy for you to say, this is the worse it can possibly get. My own friend is a dishonest cheater!"

            scene v16s30_19 # FPP. Same positioning as v16s30_15a, MC looking at Chris, Chris looking down at Imre, Chris very angry, mouth open, fists still up
            with dissolve

            ch "Haven't you said enough bullshit already, Imre? Shut the fuck up, already."

            scene v16s30_20 # FPP. Same positioning as v16s30_15a, MC looking at Sebastian, Sebastian looking at Chris, Sebastian concerned, mouth open
            with dissolve

            se "Chris, stop. He's one of us."

            scene v16s30_19a # FPP. Same as v16s30_19, Chris drops his fist, regretful expression, mouth closed, looking at Imre
            with dissolve

            ch "..."

            scene v16s30_19b # FPP. Same as v16s30_19a, Chris looking at Sebastian, Chris sorry expression, mouth open
            with dissolve

            ch "You're right."

            ch "I..."

            scene v16s30_19c # FPP. Same as v16s30_19b, Chris looking down, embarassed and sad, mouth open
            with dissolve

            ch "I'm sorry I reacted like that."

    scene v16s30_21 # TPP. Chris walking out of the room, embarassed and sad, mouth open
    with dissolve

    ch "Fuck..."

    scene v16s30_22 # TPP. Sebastian helping Imre up on his feet, MC stands watching, Imre wiping blood off his nose, Sebastian and MC worried, mouths closed
    with dissolve

    pause 0.75

    scene v16s30_23 # FPP. MC, Imre and Seb standing in the living room, MC looking at Imre, Imre looking at Sebastian, Imre annoyed, mouth open
    with dissolve

    imre "Fucking clown."

    scene v16s30_24 # FPP. Same psoitioning as v16s30_23, MC looking at Seb, Seb looking at Imre, Seb worried, mouth open
    with dissolve

    se "Imre-"

    scene v16s30_23
    with dissolve

    imre "No, Seb. No peace-making right now, you know what he did is fucked up. Both of you do."

    scene v16s30_23a # FPP. Same as v16s30_23, Imre looking at MC, annoyed, mouth open
    with dissolve

    imre "I'm out of here. And don't wait up, I don't know when I'll be back."

    scene v16s30_25 # TPP. Imre storming out, annoyed, mouth closed
    with dissolve

    u "(What the hell just happened?)"

    scene v16s30_24a # FPP. Same as v16s30_24, Seb looking at MC, Seb sad, mouth open
    with dissolve

    se "I'm gonna go meditate..."

    scene v16s30_26 # TPP. Sebastian walks away, sad, mouth closed
    with dissolve

    u "(Looks like Imre and Chris are both going through some shit right now, and they clearly don't know how to handle it.)"

    scene v16s30_27 # TPP. MC standing alone in the living room, confused, mouth closed
    with dissolve

    u "(Hopefully they can come back from this. I can't believe Chris punched him...)"

    scene v16s30_27a # TPP. Same as v16s30_27, MC mouth open, confused, walking towards the door
    with dissolve

    u "I guess I'll leave too, ha."

    scene v16s30_28 # TPP. Show MC going up to his room, confused, mouth closed
    with dissolve

    pause 0.75

    jump v16s32