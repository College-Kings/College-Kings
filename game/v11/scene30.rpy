# SCENE 30: MC and Lauren head to the store
# Location: Hotel Lobby, Sidewalk, HP Shop, Park
# Characters: MC (Outfit 9), Lauren (Outfit 1), Lindsey (Outfit 1)
# Time: Morning

label v11_lauren_store:
    scene v11laa1
    with fade
    
    pause 0.5
    
    scene v11las1 # TPP. Lauren is in the lobby, she is looking at her phone, show MC sneaking up on her
    with fade
    play music "music/v11/Track Scene 1_1.mp3" fadein 2
    pause 0.75

    scene v11las1a # TPP. Same cam as v11las1, MC is behind Lauren, he is smiling, mouth closed, his hands over her eyes, Lauren is surprised, mouth closed
    with dissolve

    pause 0.75

    scene v11las1b # TPP. Same as v11las1a, MC mouth open, slight smile, Lauren slight surprise, mouth closed
    with dissolve

    u "Guess who?"

    scene v11las1c # TPP. Same as v11las1b, MC mouth closed, slight smile, Lauren slightly confused, mouth open
    with dissolve

    la "...Dobby?"

    scene v11las1d # TPP. Same as v11las1c, MC mouth closed, slight smile, Lauren slight smile, mouth open, MC no longer covering her eyes, she still has her back turned to MC
    with dissolve

    pause 0.75

    scene v11las2 # FPP. Same character positioning as v11las1a, Lauren now looking at MC, slight smile, mouth closed
    with dissolve

    u "Who's Dobby? *Chuckles*"

    scene v11las2a # FPP. Same as v11las2, Lauren slight smile, mouth open
    with dissolve

    la "It's a Hotter Parry thing... only cool people would get that reference."

    scene v11las2
    with dissolve

    u "Um, I'm cool."

    scene v11las2a
    with dissolve

    la "Then that means you know about Hotter Parry, right?"

    scene v11las2
    with dissolve

    menu:
        "Of course":
            scene v11las2b # FPP. Same as v11las2, Lauren slightly surprised and slightly smiling, mouth closed
            with dissolve

            u "Of course I do."

            scene v11las2c # FPP. Same as v11las2, Lauren slightly surprised and slightly smiling, mouth open
            with dissolve

            la "Really?"

            scene v11las2b
            with dissolve

            u "Yeah, I know all about it."

            scene v11las2c
            with dissolve

            la "Hmm... Well, there's only one way to find out."

        "Not really":
            scene v11las2d # FPP. Same as v11las2, Lauren slightly suspicious and slight smile, mouth closed
            with dissolve

            u "Not really, no, but that doesn't mean I'm not cool."

            scene v11las2e # FPP. Same as v11las2, Lauren slightly suspicious and slight smile, mouth open
            with dissolve

            la "I feel like you know more than you're letting on... but there's only one way to find out."

    scene v11las2
    with dissolve

    u "And what's that?"

    scene v11las2a
    with dissolve

    la "First, a question. Do you have any plans today?"

    scene v11las2
    with dissolve

    u "No, not really."

    scene v11las2a
    with dissolve

    la "Good. Second thing, come with me."

    scene v11las2f # FPP. Same cam as v11las2, MC sees Lauren walking off towards hotel entrance
    with dissolve

    pause 0.75
    
    scene v11las3 # TPP. Show Lauren walking out the hotel entrance, slight smile, mouth closed, MC following her, he has mouth closed, slight smile
    with dissolve

    pause 0.75

    scene v11las4 # TPP. Show MC and Lauren walking next to each other in the sidewalk, both mouths closed, slight smiles
    with fade

    pause 0.75

    scene v11las5 # FPP. MC and Lauren standing next to each other on the sidewalk, MC and Lauren looking at each other, Lauren slight smile, mouth closed
    with dissolve

    u "Where are we going?"

    scene v11las5a # FPP. Same as v11las5, Lauren slight smile, mouth open
    with dissolve

    la "We're going to Hogwash. *Chuckles*"

    scene v11las5
    with dissolve

    u "Right... and where are we actually going?"

    scene v11las5a
    with dissolve

    la "Isn't it more exciting when it's a surprise?"

    scene v11las5
    with dissolve

    u "I guess it depends where you're taking me."

    scene v11las5a
    with dissolve

    la "Well, any Hotter Parry fan would be thrilled to go where I'm taking you."

    scene v11las5
    with dissolve

    u "What if I'm not a fan?"

    scene v11las5a
    with dissolve

    la "Then you'll probably look really out of place and embarrass yourself. *Laughs*"

    scene v11las5
    with dissolve

    u "*Sarcastic* Oh wow, sounds like fun."

    scene v11las5b # FPP. Same as v11las5, Lauren puppy eyes, mouth open
    with dissolve

    la "Oh come on, [name]. You're going to love it. Trust me."

    scene v11las6 # TPP. Show MC and Lauren walking in the sidewalk again (different place than v11las4)
    with dissolve

    pause 0.75

    scene v11las7 # TPP. Show MC and Lauren standing in front of the building, looking at it
    with dissolve

    pause 0.75

    scene v11las8 # FPP. MC and Lauren standing next to each other in front of the building, looking at each other, Lauren smile, mouth open
    with dissolve

    la "And... look! We're here."

    scene v11las9 # TPP. Show MC and Lauren walking in the building door, Lauren smiling, mouth closed, MC surprised, mouth open (but not talking, mouth open from the surprise)
    with dissolve

    pause 1.25

    scene v11las10 # TPP. Show MC and Lauren from behind, standing next to each other, 4 tables in shot, one has the judges sitting down, one is empty, the one to the right of MC's seat on the empty table has Jerry sitting down, add other people in the other tables (max 2 per table)
    with dissolve

    pause 1.25

    scene v11las11 # FPP. MC and Lauren standing next to each other, same positioning as v11las10, looking at each other, Lauren slight smile, mouth closed
    with dissolve

    u "What the fuck is this?"

    scene v11las11a # FPP. Same as v11las11, Lauren slight smile, mouth closed
    with dissolve

    la "*Chuckles* Oh my gosh [name]! Not so loud... It's a Hotter Parry trivia contest. I signed up for it."

    scene v11las11
    with dissolve

    u "So, that's why we spent the last 15 minutes talking about Hotter Parry?"

    scene v11las11a
    with dissolve

    la "Hotter Parry was the only thing on my mind. I don't know if you've noticed, but we're in Europe... The birthplace of Hotter Parry?"
    
    la "There's a ton of cool events going on and I really wanted to go to this one. Also why did you think I was standing in the lobby?"

    scene v11las11
    with dissolve

    u "I thought you just enjoyed standing around."

    scene v11las11a
    with dissolve

    la "*Chuckles* Not funny. I had plans way before you showed up and obviously, I had to drag you here to test your Hotter Parry knowledge."

    scene v11las11b # FPP. Same as v11las11, different pose, Lauren slight smile, mouth closed
    with dissolve

    u "Didn't you sign up by yourself?"

    scene v11las11c # FPP. Same as v11las11b, Lauren slight smile, mouth open
    with dissolve

    la "Well... You can either participate as a couple or an individual. I never stated how I'd be participating."

    scene v11las11b
    with dissolve

    u "Ah. And here I am."

    scene v11las11c
    with dissolve

    la "So here you are."

    scene v11las11
    with dissolve

    u "What do we win?"

    scene v11las11a
    with dissolve

    la "If we win, they praise us and we go home. If we lose, they silently judge us and we go home."

    scene v11las11
    with dissolve

    u "*Sarcastic* Wow, so much pressure..."

    scene v11las11a
    with dissolve

    la "*Chuckles* Just do your best. I'll pick up the slack."

    scene v11las11b
    with dissolve

    u "*Chuckles* How kind of you."

    scene v11las12 # FPP. MC looks forward and sees the judge approaching MC and Lauren, judge friendly smile (judge still not in talking distance, only Judge in shot)
    with dissolve

    pause 0.75

    scene v11las12a # FPP. Same as v11las12, Judge now in talking distance, Judge slight smile, mouth open, looking at Lauren (Lauren out of shot, same positioning as v11las11)
    with dissolve

    jud "You must be Team Pofflehoof?"

    scene v11las13 # FPP. Same positioning as v11las12a, MC looking at Lauren looking at Judge direction (Judge out of shot), Lauren mouth open, smiling
    with dissolve

    la "That's us."

    scene v11las12a
    with dissolve

    jud "Great, please take a seat. We're just about to get started."

    scene v11las14 # TPP. Show MC and Lauren heading for their seats, both slightly excited, mouths closed
    with dissolve

    pause 0.75

    scene v11las15 # TPP. MC and Lauren standing by their seats, both smiling, mouths clsoed
    with dissolve

    pause 0.75

    scene v11las15a # TPP. Same camera as v11las15, MC and Lauren midway through sitting down
    with dissolve

    pause 0.75

    scene v11las16 # TPP. Show MC and Lauren sitting down in their seats, both smiling, mouths closed show the contestant to MC's right (Jerry) making a stern face, mouth closed, MC not looking at Jerry yet
    with dissolve

    pause 0.75

    scene v11las17 # FPP. Same positioning as v11las16, MC looking to his right, he sees Jerry looking at him, Jerry stern expression, mouth open, make sure the board he will write on is visible
    with dissolve

    jer "Abada Kavarkada."

    scene v11las17a # FPP. Same as v11las17, Jerry mouth closed, angry expression
    with dissolve

    u "What the fuck?"

    scene v11las18 # FPP. Same positioning as v11las16, MC and Lauren looking at each other, Lauren slightly annoyed, mouth slightly open, the board they will use is on the table
    with dissolve

    la "*Whisper* [name]!"

    scene v11las17b # FPP. Same as v11las17, MC pointing at Jerry, Jerry mouth closed, angry expression
    with dissolve

    pause 0.75

    u "That guy right there just looked at me all crazy and said Abada Kavarkada."

    scene v11las18a # FPP. Same cam as v11las18, Lauren leans over, looks at Jerry, Lauren angry, mouth closed
    with dissolve

    pause 0.75

    scene v11las18b # FPP. Lauren is waving her finger, mouth closed, angry expression
    with dissolve

    pause 0.75

    scene v11las18c # FPP. Lauren pointing her finger at Jerry, Lauren mouth open, angry expression
    with dissolve

    la "Abada Kavarkada!"

    scene v11las19 # FPP. Same positioning as v11las16, MC looking at the Judge who is seated at her table, Judge looking at MC, Judge mouth open, slightly annoyed
    with dissolve

    jud "Students please... control yourselves."

    scene v11las18d # FPP. Same as v11las18, Lauren slightly annoyed, mouth closed
    with dissolve

    u "Well, look at you... a little witch girl. *Chuckles* You guys are really serious about this stuff."

    scene v11las18a
    with dissolve

    la "We're serious about Hotter Parry, not stuff. Don't call it stuff."

    scene v11las18e # FPP. Same as v11las18, Lauren smiling, mouth closed
    with dissolve

    u "Damn, my bad. You guys are serious about Hotter Parry."

    scene v11las18f # FPP. Same as v11las18, Lauren smile, mouth open
    with dissolve

    la "Better. *Chuckles*"

    scene v11las19a # FPP. Same as v11las19, Judge neutral expression, mouth open
    with dissolve

    jud "Attention, students. It brings me great joy to see you all here. It's always wonderful to see new faces, although I see a few familiar ones..."

    scene v11las19b # FPP. Same as v11las19a, Judge pointing and looking towards Jerry (Jerry out of shot), Judge neutral expression, mouth open
    with dissolve

    jud "Jerry."

    scene v11las17c # FPP. Same as v11las17, Jerry looking towards Judge direction, mouth open, very excited
    with dissolve

    jer "I'm a vet!"

    scene v11las19a
    with dissolve

    jud "Okay, so... The rules are simple. I will ask three main questions and one bonus question."
    
    jud "The bonus question is so important that it's worth three points while the others are only worth one point."

    scene v11las19c # FPP. Same as v11las19, Judge mouth closed, neutral expression
    with dissolve

    u "Wait, so in the event that we get the first three wrong-"

    scene v11las17d # FPP. Same as v11las17, Jerry huge grin, mouth open
    with dissolve

    jer "You will."

    scene v11las19c
    with dissolve

    u "*Sighs* In the event that we get the first three wrong and the bonus right, assuming someone else gets the first three right and the bonus wrong, does that mean we have a tie?"

    scene v11las19a
    with dissolve

    jud "Exactly. We've only ever had one tie before."

    scene v11las18f
    with dissolve

    la "*Whisper* We won't be losing [name]."

    scene v11las18e
    with dissolve

    u "Well, I'm glad you're confident."

    scene v11las19a
    with dissolve

    jud "No phones, no notes, no nothing. You will write your answer on the board in front of you and show us after everyone is finished. Let's begin."

    scene v11las18f
    with dissolve

    la "Let's do this."

    scene v11las19d # FPP. Same as v11las19, Judge is looking at a piece of paper, slight smile, mouth open
    with dissolve

    jud "First question, what House is Hotter Parry a member of?"

    scene v11las17e # FPP. Same as v11las17, Jerry writing down and looking on his board, mouth open, big smile
    with dissolve

    jer "Too easy!"

    scene v11las18f
    with dissolve

    la "*Whisper* Good god, he's almost as annoying as Charli."

    scene v11las18e
    with dissolve

    u "Haha."

    u "(So I'm not the only one who thinks he's annoying.)"

    scene v11las18g # FPP. Same as v11las18, Lauren holding and looking at the board, pen on hand, slightly smiling, mouth closed
    with dissolve

    menu (fail_label="v11_answer_Theremin"):
        "Friggindoor":
            $ lauren.points += 1
            $ v11_hp_points += 1

            scene v11las18h # FPP. Same as v11las18g, Lauren smiling, mouth closed, looking at MC
            with dissolve

            u "It's Friggindoor, right?"

            scene v11las18i # FPP. Same as v11las18h, Lauren smiling, mouth open
            with dissolve

            la "*Whisper* Sure is."

            jump v11_quiz_q2

        "Theremin":
            jump v11_answer_Theremin

label v11_answer_Theremin:
    $ lauren.points -= 1

    scene v11las18j # FPP. Same as v11las18g, Lauren looking at MC, she is slightly disappointed, mouth closed
    with dissolve

    u "*Whisper* It's Theremin, right?"

    scene v11las18k # FPP. Same as v11las18j, Lauren slightly smiling, mouth open
    with dissolve

    la "Uhhh, no. *Chuckles* I got this."

    jump v11_quiz_q2

label v11_quiz_q2:

    scene v11las19a
    with dissolve

    jud "Everyone ready?"

    scene v11las19c
    with dissolve

    u "We are."

    scene v11las17c
    with dissolve

    jer "I'm prepared, as always."

    scene v11las17f # FPP. Same as v11las17c, Jerry mouth closed, very excited
    with dissolve

    u "(Such a dick.)"

    scene v11las20 # FPP. MC still sitting down, looking at Susan (she's sitting at her table), Susan slightly smiling, mouth open
    with dissolve

    sus "Ready!"

    scene v11las19a
    with dissolve
    
    jud "Perfect. Please show us your answers."

    scene v11las18lxx # FPP. Same as v11las18g, Lauren looking at Judge, Lauren holding up the board (Friggindoor written on the board), smiling, mouth closed
    with dissolve

    pause 0.75

    scene v11las19e # FPP. Same as v11las19, Judge looking at MC, mouth open, slight smile
    with dissolve

    jud "Hmmm... It appears everyone is correct. Very good."

    scene v11las19d
    with dissolve
    
    jud "Next question, how many kahoots are in one shtickle?"

    scene v11las19f # FPP. Same as v11las19d, Judge mouth closed, slight smile
    with dissolve

    menu (fail_label="v11_answer_13"):
        "13":
            jump v11_answer_13

        "29":
            $ lauren.points += 1
            $ v11_hp_points += 1

            scene v11las18h
            with dissolve

            u "*Whisper* Too easy, it's 29."

            scene v11las18i
            with dissolve

            la "You're pretty confident... This isn't a beginner question, you know?"

            scene v11las18h
            with dissolve

            u "I'm not a beginner..."

            jump v11_quiz_q3
        
label v11_answer_13:
    $ lauren.points -= 1

    scene v11las18j
    with dissolve

    u "*Whisper* I have no idea... but I'd guess 13. I was gonna say 29, but that seems too high, right?"

    scene v11las18k
    with dissolve

    la "*Whisper* High or not, 29 is the right answer. I'm starting to think you may not be such an expert. *Chuckles*"

    scene v11las18m # FPP. Same as v11las18k, Lauren slight smile, mouth closed
    with dissolve

    u "*Whisper* Haha, maybe not."

    jump v11_quiz_q3

label v11_quiz_q3:
    scene v11las17g # FPP. Same as v11las17a, Jerry mouth open, angry
    with dissolve

    jer "I believe we're ready to continue, if the love birds here are finished with their off-topic conversation?"
    
    jer "By all means, it's his bitch- I mean... witchy girlfriend that is the only true Hotterhead."

    scene v11las19g # FPP. Same as v11las19, Judge looking at Jerry, she is slightly angry, mouth open
    with dissolve

    jud "Jerry, please."

    if lauren.relationship >= Relationship.GIRLFRIEND:
        scene v11las21 # TPP. Show MC getting up from his chair, MC is very angry, mouth closed
        with dissolve

        pause 0.75

        scene v11las22 # TPP. MC standing up, leaning towards Jerry, show Lauren holding him back
        with dissolve

        pause 0.75

        scene v11las23 # FPP. MC is standing up, looking at Jerry (sitting down), Jerry slightly scared, mouth closed
        with dissolve

        u "Did you just call my girl a bitch?"

        scene v11las23a # FPP. Same as v11las23, Jerry angry, mouth open
        with dissolve

        jer "Oh, calm down bulldog... I simply misspoke."

        scene v11las24 # FPP. MC standing up in front of his seat, he is looking at the Judge, Judge looking at him, gesticulating for him to take a seat, she is slightly annoyed, mouth open
        with dissolve

        jud "Please, just have a seat sir."

        scene v11las25 # TPP. Show MC sitting down, he is angry, mouth closed
        with dissolve

        pause 0.75

        scene v11las18f
        with dissolve

        la "My little hero, huh?"

        scene v11las18e
        with dissolve

        u "Well, I'm not just gonna let him talk to you like that."

    else:
        scene v11las19g
        with dissolve

        jud "Any other comments like that and I will have you disqualified."

        scene v11las17h # FPP. Same as v11las17c, Jerry angry, mouth open
        with dissolve

        jer "Oh please... I've won this contest a thousand times and know that unless it's stated in the rules, I can't be disqualified."

        scene v11las19g
        with dissolve

        jud "I will make up rules as necessary."

        scene v11las17a
        with dissolve

        u "Be careful man, you're walking on thin ice."

    scene v11las19a
    with dissolve

    jud "May I see everyone's answers, please?"

    scene v11las18n # FPP. Same as v11las18l, but "29" is written on the board instead of "Friggindoor"
    with dissolve

    pause 0.75

    scene v11las19e
    with dissolve

    jud "It appears everyone is correct except Susan."

    scene v11las20a # FPP. Same as v11las20, Susan is now standing, mouth open, very angry, she is looking at the judge's direction
    with dissolve

    sus "FUCK!"

    scene v11las20b # FPP. Same as v11las20a, Show Susan flipping the table over, she is very angry mouth closed
    with dissolve

    pause 0.75

    scene v11las20c # FPP. Same as v11las20b, table now flipped over, Susan walking away
    with dissolve

    pause 0.75

    scene v11las18h
    with dissolve

    u "*Whisper* Only the cool kids like Hotter Parry, right? *Laughs*"

    scene v11las18i
    with dissolve

    la "*Whisper* Maybe I was wrong..."

    scene v11las18h
    with dissolve

    u "*Whisper* Don't worry, you're still cool."

    scene v11las17h
    with dissolve

    jer "One down, one to go. And may I point out that once again, team Pofflehoof continues to whisper amongst themselves? I'd like to be sure they aren't cheating..."

    scene v11las19g
    with dissolve

    jud "What you are sure of is irrelevant Jerry, as they have broken zero rules and are doing an amazing job. So let us continue."

    scene v11las19d
    with dissolve
    
    jud "Please tell me, where is the Order of the Albatross headquarters located?"

    scene v11las18f
    with dissolve

    la "*Whisper* I'm sorry [name], but I really have to use the ladies' room. This one is all you."

    scene v11las18e
    with dissolve

    u "*Whisper* Wow."

    scene v11las18f
    with dissolve

    la "*Whisper* Don't screw it up. *Chuckles*"

    scene v11las26 # FPP. MC is looking at the empty board he is going to write on
    with dissolve

    menu (fail_label="v11_answer_ministry"):
        "12 Someold Place":
            $ lauren.points += 1
            $ v11_solo_question = True
            $ v11_hp_points += 1

            scene v11las26axx # FPP. Same as v11las26, but "12 Someold Place" is written on the board
            with dissolve

            u "(This better be right.)"

            jump v11_quiz_bonus

        "Ministry of Fisheries":
            jump v11_answer_ministry

label v11_answer_ministry:
    $ lauren.points -= 1

    scene v11las26bxx # FPP. Same as v11las26, but "Ministry of Fisheries" is written on the board
    with dissolve

    u "(I hope this is right.)"

    jump v11_quiz_bonus
        
label v11_quiz_bonus:
    scene v11las17d # FPP. Same as v11las17, Jerry holding his board, looking at MC, Jerry smug face, mouth open
    with dissolve

    jer "*Chuckles* Someone looks all flustered without their little girlfriend."

    scene v11las17a # FPP. Same as v11las17i, Jerry smug face, mouth closed
    with dissolve

    u "Bro, what is your deal? Just enjoy the game and have a good time like everyone else."

    scene v11las17d
    with dissolve

    jer "I'm enjoying myself quite fine."

    scene v11las17d
    with dissolve

    u "*Sighs*"

    scene v11las17e
    with dissolve

    u "(Fucking prick.)"

    scene v11las19a
    with dissolve

    jud "Are you ready to show us your answers, gentlemen?"

    scene v11las19c
    with dissolve

    u "Sure."
    
    if v11_solo_question: # If answered correctly
        scene v11las28 # TPP. Show MC sitting on his chair, looking down at the board, he is smiling, mouth closed, Lauren walking in behind him, she is smiling, mouth closed
        with dissolve

        pause 0.5

        scene v11las28a # TPP. Same as v11las28, MC now looking at Lauren, Lauren midway through sitting down, both smiling, mouths closed
        with dissolve

        pause 0.5

        scene v11las27xx # TPP. Show MC holding up the board, MC neutral expression, mouth closed, "12 Someold Place" written on the board (Like in v11las26a)
        with dissolve

        pause 0.75

        scene v11las19a
        with dissolve

        jud "It appears you are both correct."

        scene v11las18f
        with dissolve

        la "*Whisper* Way to hold down the fort! Thanks."

        scene v11las18e
        with dissolve

        u "*Whisper* Just a professional at work. *Chuckles*"

        scene v11las18f
        with dissolve

        la "*Whisper* Haha, focus! Bonus question time..."

    else: # If answered incorrectly
        scene v11las28 # TPP. Show MC sitting on his chair, looking down at the board, he is smiling, mouth closed, Lauren walking in behind him, she is smiling, mouth closed
        with dissolve

        pause 0.5

        scene v11las28a # TPP. Same as v11las28, MC now looking at Lauren, Lauren midway through sitting down, both smiling, mouths closed
        with dissolve

        pause 0.5

        scene v11las27axx # TPP. Same as v11las27, but "Ministry of Fisheries" written on the board (like in v11las26b)
        with dissolve

        pause 0.75

        scene v11las19a
        with dissolve

        jud "It appears that only Jerry got this one correct."

        scene v11las17i
        with dissolve

        jer "As expected."

        #scene v11las28b # TPP. Same as v11las28, MC looking down, slightly sad, mouth closed, Lauren walking behind him, she is slightly disappointed, mouth closed
        #with dissolve

        #pause 0.75

        #scene v11las28c # TPP. Same as v11las28a, but both slightly disappointed, mouths closed
        #with dissolve

        #pause 0.75

        scene v11las18f
        with dissolve

        la "*Whisper* Got a little lost without me, huh?"

        scene v11las18e
        with dissolve

        u "*Whisper* Just a little bit. *Chuckles*"

        scene v11las18f
        with dissolve

        la "*Whisper* It's alright, bonus question time..."

    scene v11las19a
    with dissolve

    jud "I have a Doctor When trivia event starting soon, so on to the last question: The bonus question."

    if v11_solo_question:
        
        jud "Since we are tied, in order for a win, one team must be correct and the other must get it incorrect."

        scene v11las17d
        with dissolve

        jer "You two may leave now if you'd like."

        scene v11las17d
        with dissolve

        u "After you, Jerry."

    else:

        jud "To win, Team Pofflehoof must get the answer correct, and Jerry must not."

        scene v11las17c
        with dissolve

        jer "Impossible."

        scene v11las17d
        with dissolve

        u "*Sighs*"

    scene v11las19d
    with dissolve

    jud "So, the final question. What kind of dragon protected the vaults at Greengoat Bank?"

    scene v11las17k # FPP. Same as v11las17h, Jerry slightly flustered, mouth open
    with dissolve

    jer "Will you repeat the question?"

    scene v11las17
    with dissolve

    u "There a problem Jerry? *Chuckles*"

    scene v11las17a
    with dissolve

    jer "*Groan* Nevermind, I know the answer."

    scene v11las18f
    with dissolve

    la "*Whisper* I got this one, [name]."

    scene v11las18e
    with dissolve

    u "*Whisper* You sure?"

    scene v11las18f
    with dissolve

    la "*Whisper* Oh... I'm more than sure."

    scene v11las19a
    with dissolve

    jud "Are we ready?"

    scene v11las18oxx # FPP. Same as v11las18, board is on the table ("Ukrainian Ironbelly" written on the board), Lauren mouth open, smiling, looking at Judge
    with dissolve

    la "Yes, ma'am. We're ready."

    scene v11las17l # FPP. Same as v11las17, Jerry looking down at his phone, mouth open, slightly flustered (He is trying to hide the phone, but make it visible)
    with dissolve

    jer "One moment."

    scene v11las19g
    with dissolve

    jud "JERRY! *Deep breath* Please, put your phone away."

    scene v11las17m # FPP. Same as v11las17c, Jerry slightly flustered, mouth open
    with dissolve

    jer "Uhh, sorry. My mother... she um, sorry."

    scene v11las19g 
    with dissolve

    jud "Answers please."

    scene v11las18pxx # FPP. Same as v11las18l, but "Ukrainian Ironbelly" written on the board
    with dissolve

    pause 0.75

    scene v11las19e
    with dissolve

    if v11_hp_points == 3:
        $ grant_achievement("earn_your_owl")

    jud "For the first time in a very stressful two months, Jerry you have lost. Congratulations Team Pofflehoof!"

    scene v11las17h
    with dissolve

    jer "No! This isn't fair, they must've cheated! I want a redo right now, there's no way they knew that and I didn't."

    scene v11las19g
    with dissolve

    jud "Security, please escort this man outside."

    scene v11las29 # TPP. Show the security guard (same as scene 5, don't show his face), escorting Jerry out
    with dissolve

    pause 0.75

    scene v11las18e
    with dissolve

    u "What a shame, he was starting to grow on me."

    scene v11las18f
    with dissolve

    la "Haha, I bet he was."

    scene v11las30 # TPP. MC and Lauren talking to each other, show Judge walking up to them, Judge is smiling, mouth closed
    with dissolve

    pause 0.75

    scene v11las31 # FPP. Judge standing in front of MC and Lauren (they're sitting down), MC looking at Judge, Judge smiling, mouth open
    with dissolve

    jud "You are both very good sports, thank you for participating. Please feel free to explore the assortment of fan material we have."

    scene v11las18q # FPP. Same as v11las18, Lauren looking at the Judge (standing in front of them, positioning like v11las31), Lauren smiling, mouth open
    with dissolve

    la "Thank you very much."

    scene v11las31a # TPP. Same as v11las31, MC and Lauren looking at the Judge as the judge walks away, all smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11las32 # TPP. Show Lauren and MC hugging each other (they're both still sitting down), both smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11las18f
    with dissolve

    la "I'm glad we won, but it feels even better knowing we beat that jerk."

    if lauren.relationship >= Relationship.GIRLFRIEND:
        play sound "sounds/kiss.mp3"
        scene v11las33 # TPP. Show Lauren kissing MC, they're both still sitting down
        with dissolve

        pause 1.5

        scene v11las18f
        with dissolve

        la "And that's for sticking up for me."

        scene v11las18e
        with dissolve

        u "I would never let anyone disrespect you."

        scene v11las18f
        with dissolve

        la "*Chuckles* You're so sweet."

    else:
        scene v11las18e
        with dissolve

        u "It took everything in me not to jump across the table when he started name-calling."

        scene v11las18f
        with dissolve

        la "You did just fine."

    scene v11las18e
    with dissolve

    la "Let's check out the books."

    scene v11las18f
    with dissolve

    u "Sounds good."

    scene v11las34 # TPP. Show MC and Lauren getting up from the chair, both smiling, mouths up
    with dissolve

    pause 0.75

    scene v11las34a # TPP. Same cam as v11las34 MC and Lauren now standing up, walking towards the bookcase, both smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11las35 # TPP. Show MC and Lauren walking towards the bookcase, both smiling, mouths closed (they're close to it now)
    with dissolve

    pause 0.75

    scene v11las36 # TPP. Show MC and Lauren standing in front of the bookcase, shot the HP books in the bookcase
    with dissolve

    pause 0.75

    scene v11las37 # FPP. MC and Lauren standing in front of tghe bookcase, MC and Lauren looking at each other, Lauren smiling, mouth open
    with dissolve

    la "Question."

    scene v11las37a # FPP. Same as v11las37, Lauren mouth closed, smiling
    with dissolve

    u "Answer."

    scene v11las37
    with dissolve

    la "*Chuckles* Okay, how many HP books are there?"

    scene v11las37a
    with dissolve

    menu:
        "7":
            $ lauren.points += 1

            scene v11las37a
            with dissolve

            u "I don't know, seven?"

            scene v11las37
            with dissolve

            la "Yep, but there's eight movies."

        "8":
            $ lauren.points -= 1

            scene v11las37b # FPP. Same as v11las37, Lauren slightly disappointed, mouth closed
            with dissolve

            u "Hmm, 8 right?"

            scene v11las37c # FPP. Same as v11las37b, Lauren slightly disappointed, mouth open
            with dissolve

            la "Nope, there's eight movies, but there's only seven books. The last book was cut into two parts for the movie."
        
    scene v11las37a
    with dissolve

    u "Someone really knows their stuff..."

    scene v11las37
    with dissolve

    la "*Chuckles* We're all fans of something."

    scene v11las38 # TPP. MC looking at the bookcase, Lauren looking to where the robes are, both smiling, Lauren mouth open, MC mouth closed
    with dissolve

    la "Oh my gosh, look look look!"

    scene v11las38a # TPP. Same as v11las38, MC now looking towards where the robes are, MC smiling, mouth open, Lauren mouth closed, looking at the robes, smiling (not pointing anymore)
    with dissolve

    u "What?"

    scene v11las38
    with dissolve

    la "They have robes here. C'mon."

    scene v11las39 # TPP. Show Lauren pulling MC by the arm, both smiling, mouths closed, moving towards where the robes are
    with dissolve

    pause 0.75

    scene v11las40 # FPP. Show Lauren placing the robe on her, she is smiling, mouth closed (they~re at where the robes are)
    with dissolve

    pause 0.75

    scene v11las40a # FPP. Same as v11las40, Lauren now has the robe completely on, she is looking at MC, smiling, mouth open
    with dissolve

    la "Ahhh, this looks too cool! Here, here!"

    scene v11las41 # TPP. Show Lauren putting the robe on MC, both smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11las40a
    with dissolve

    la "I love it! [name] The Wizard!"

    scene v11las40b # FPP. Same as v11las40a, Lauren smiling, mouth closed
    with dissolve

    u "I don't think I've ever seen you this happy."

    scene v11las40a
    with dissolve

    la "I don't think I've ever been this happy *Chuckles*"

    scene v11las40c # FPP. Same as v11las40a, Lauren looking towards the Judge (Judge is behind MC, on the other side of the room)
    with dissolve

    la "EXCUSE ME, MA'AM?"

    scene v11las42 # TPP. Show the Judge looking at MC and Lauren, Lauren waving the Judge to come over, Judge slightly surprised, Lauren and MC smiling, all mouths closed
    with dissolve

    pause 0.75

    scene v11las42a # TPP. Same as v11las42, show the Judge walking over, MC, Lauren and Judge smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11las43 # FPP. MC now looking at the Judge, Judge mouth closed, smiling
    with dissolve

    jud "How can I help you?"

    scene v11las44 # FPP. MC standing next to Lauren, looking at her, Lauren looking at Judge (Judge out of shot), Lauren smiling, mouth open
    with dissolve

    la "Could you please take our picture?"

    scene v11las44a # FPP. Same as v11las44, Lauren now looking at MC, slightly disappointed, mouth closed
    with dissolve

    u "Oh, she doesn't have to do that."

    scene v11las43
    with dissolve

    jud "I'd be happy to! Please, squish together a bit."

    if lauren.relationship >= Relationship.GIRLFRIEND:
        play sound "sounds/kiss.mp3"
        scene v11las45 # TPP. Show MC and Lauren posing for a picture, Lauren has her arms wrapped around MC's neck and they're kissing
        with dissolve

        pause 1.5

        scene v11las43a # FPP. Same as v11las43, Judge holding up the phone as if she were taking the pic, Judge mouth open, smiling
        with dissolve

        jud "Wow, such a lovely couple."

    scene v11las43a
    with dissolve

    jud "Let's get a good one with these."

    scene v11las43b # FPP. Same as v11las43, Judge holding out 2 wands with one of her hands, the phone is on the other, Judge smiling, mouth closed
    with dissolve

    pause 0.75

    scene v11las44b # FPP. Same as v11las44, Lauren looking at MC, big smile, mouth open, she is holding up her wand
    with dissolve

    la "Oh my gosh, this is turning out to be the best day ever..."

    scene v11las44c # FPP. Same as v11las44b, Lauren big smile, mouth closed
    with dissolve

    u "Haha."

    scene v11las45a # TPP. Same cam as v11las45, MC and Lauren posing for a picture, both smiling, mouths closed, wands in hand
    with dissolve

    pause 1

    scene v11las43a
    with dissolve

    jud "Just perfect! Here's your phone... Please, keep the robe and the wands as a reward for winning the contest and being such wonderful contestants."

    scene v11las44
    with dissolve

    la "Oh my, thank you so much!"

    scene v11las43c # FPP. Same v11las43, Judge looking at Lauren, Judge mouth open, smiling
    with dissolve

    jud "No, thank you."

    scene v11las43d # FPP. Same cam as v11las43, Judge walking away
    with dissolve

    pause 0.75

    scene v11las44d # FPP. Same as v11las44b, Lauren not holding up the wand, big smile, mouth open (diff pose)
    with dissolve

    la "This day couldn't have been better, [name]. Thank you so much for coming with me."

    scene v11las44e # FPP. Same as v11las44d, Lauren big smile, mouth closed
    with dissolve

    u "Wouldn't have missed it for the world."

    if (lauren.relationship >= Relationship.GIRLFRIEND or (kct == "loyal" and lauren.relationship >= Relationship.KISS)) and not "v11_aubrey" in sceneList:
        if lauren.relationship < Relationship.GIRLFRIEND:
            call screen kct_popup

        scene v11las44f # FPP. Lauren has her hands around MC's neck, she is looking into his eyes, she is nervous, mouth open
        with dissolve

        la "[name]."

        scene v11las44g # FPP. Same as v11las44f, Lauren mouth closed, she is nervousS
        with dissolve

        u "Yes?"

        scene v11las44f
        with dissolve

        la "I... I love you."

        scene v11las44g
        with dissolve

        menu:
            "I love you too":
                $ lauren.relationship = Relationship.GIRLFRIEND
                $ add_point(KCT.BOYFRIEND)
                $ lauren.points += 2

                scene v11las44h # FPP. Same as v11las44g, Lauren mouth closed, big smile
                with dissolve

                u "I love you too, Lauren."

                play sound "sounds/kiss.mp3"

                scene v11las46 # TPP. MC and Lauren passionately kissing
                with dissolve

                pause 1

            "Play it off":
                $ add_point(KCT.BRO)
                $ lauren.points -= 2

                scene v11las44i # FPP. Same as v11las44g, Lauren mouth closed, sad and embarrassed, no longer holding MC's neck
                with dissolve

                u "*Laughs* Someone's under a love spell."

                scene v11las44j # FPP. Same as v11las44i, Lauren mouth open, sad and embarrassed, no longer holding MC's neck
                with dissolve

                la "Um... haha, yeah."

    scene v11las44k # FPP. Same as v11las44d, Lauren slight smile, mouth open
    with dissolve
    stop music fadeout 3
    play music "music/v11/Track Scene 17.mp3" fadein 2
    la "If it was my choice I'd spend all day with you exploring Hogwash, but I promised Riley I'd help her out with something, so I have to run."

    scene v11las44l # FPP. Same as v11las44k, Lauren slight smile, mouth closed
    with dissolve

    u "No worries, I'll see you. Here."

    scene v11las47 # TPP. Show MC removing the robe, Lauren and MC slight smiles, mouths closed
    with dissolve

    pause 0.75

    scene v11las47a # TPP. Same as v11las47, MC handing Lauren the robe and the wand, both slight smile, mouths closed
    with dissolve

    pause 0.75

    scene v11las44m # FPP. Same as v11las44l, Lauren holding MC's robes, Lauren slight smile, mouth open
    with dissolve

    la "Thanks, I'll send you our pictures."

    scene v11las48 # TPP. Show Lauren walking away, she is carrying MC's robes, she is still wearing her robes, both smiling, mouths closed
    with dissolve

    pause 0.75

    if lauren.relationship >= Relationship.GIRLFRIEND:
        scene v11las49
        with dissolve

        u "(She said she loved me.)"
        
        play sound "sounds/vibrate.mp3"

        u "(Probably our pictures.)"

        $ lauren.messenger.newImgMessage("images/v11/Scene 30/Laurentxtimg1.webp", force_send=True)
        $ lauren.messenger.newImgMessage("images/v11/Scene 30/Laurentxtimg2.webp") # Lauren and MC picture together kissing (check v11las45 for the pose)
        $ lauren.messenger.addReply("Some good looking people.")
        $ lauren.messenger.newMessage("Sure are.")

        label v11s30_PhoneContinuelauren2:
            if lauren.messenger.replies:
                call screen phone
            if lauren.messenger.replies:
                u "(I should check my phone.)"
                jump v11s30_PhoneContinuelauren2

    else:
        scene v11las49 # TPP. Show MC standing in the aisle, looking at where Lauren was walking away from, he is slightly smiling, mouth closed
        with dissolve

        u "(Wow, that was really nice.)"

        play sound "sounds/vibrate.mp3"

        u "(Probably our picture.)"

        $ lauren.messenger.newImgMessage("images/v11/Scene 30/Laurentxtimg1.webp", force_send=True) # Lauren and MC picture together holding wands (check v11las45a for the pose)
        $ lauren.messenger.addReply("Some good looking people.")
        $ lauren.messenger.newMessage("Sure are.")

        label v11s30_PhoneContinuelauren1:
            if lauren.messenger.replies:
                call screen phone
            if lauren.messenger.replies:
                u "(I should reply to Lauren.)"
                jump v11s30_PhoneContinuelauren1

    scene v11las49
    with dissolve

    u "(That's how you start a day off right. Let's see what else today has to offer.)"

    scene v11las50 # TPP. Show MC walking out of the shop, he is slightly smiling, mouth closed
    with dissolve

    pause 0.75

    scene v11las51 # TPP. Show MC walking along the sidewalk, right outside the shop, he is slightly smiling, mouth closed
    with dissolve

    pause 0.75

    scene v11las52 # TPP. Show MC walking along the sidewalk near the park (show the park in the background), MC is slightly smiling, mouth closed
    with fade

    pause 0.75

    scene v11las53 # FPP. MC can see Lindsey sitting in a park bench, she is slightly sad, mouth closed
    with dissolve

    pause 0.75

    jump v11_lindsey_park