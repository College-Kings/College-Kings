# SCENE 20: Treasure Hunt
# Location: Hotel corridor/Hotel Lobby/Sidewalk/Airsoft Store/Airsoft Gun Range
# Characters: MC(outfit 1), Riley(outfit 2), Mr. Lee (Outfit 1), Nora (Outfit 3), Duncan (needs character and outfit)
# Time: Morning

label v11_treasure_hunt:
    scene v11th1 # TPP. MC and Riley are walking together in the hallway outside their room, they're smiling, mouths closed
    with dissolve
    play music music.ck1.v11.Track_Scene_20_1 fadein 2
    pause 0.75

    scene v11th2 # TPP. Show MC and Riley in the lobby, they're walking, still smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11th3 # TPP. Show MC and Riley walking out of the lobby, both smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11th4 # TPP. Show MC and Riley walking on the sidewalk, mouths closed, slightly smiling
    with dissolve

    pause 0.75

    scene v11th5 # FPP. MC and Riley on the sidewalk, looking at each other, Riley is slightly smiling, mouth open
    with dissolve

    ri "Okay, so you know how we said it was either a gun range or skeet shooting?"

    scene v11th5a # FPP. Same as v11th5, Riley mouth closed, slightly smiling
    with dissolve

    u "Yeah."

    scene v11th5
    with dissolve

    ri "Well, the only thing I could find in terms of shooting something is a place called \"Duncan's Airsofting\"."

    scene v11th5a
    with dissolve

    u "Duncan's? That has to be it, right?"

    scene v11th5
    with dissolve

    ri "It has to be. Plus, I looked up the address online and it's only about five minutes from the hotel."

    scene v11th5b # FPP. Same as v11th5a, different pose, Riley mouth closed, slight smile
    with dissolve

    u "Does this not feel too easy to be some elaborate treasure hunt?"

    scene v11th5c # FPP. Same as v11th5b, Riley mouth open, slight smile
    with dissolve

    ri "Don't be a party pooper."

    scene v11th5b
    with dissolve

    u "Okay, okay, I'm just saying... What's the riddle again?"

    scene v11th5
    with dissolve

    ri "You didn't memorize it?"

    scene v11th5b
    with dissolve

    u "I didn't know I needed to..."

    scene v11th5c
    with dissolve

    ri "*Sighs* What would you do without me?"

    scene v11th5a
    with dissolve

    u "Enjoy a few more hours of sleep."

    scene v11th5c
    with dissolve

    ri "Oh, quit whining. The riddle is \"Deep in London, you'll find Mr. Duncan, not at a luncheon, where gunmen go stunting."

    scene v11th5
    with dissolve
    
    ri "Many of targets, be careful in London, a bullet is quick and comes almost too sudden.\""

    scene v11th5a
    with dissolve

    u "I don't know if I should be impressed that you remember or think you're a little ridiculous."

    scene v11th5
    with dissolve

    ri "You should definitely be impressed."

    scene v11th6 # TPP. Show MC and Riley walking on the sidewalk, slight smiles, mouths closed, different location to v11th4
    with dissolve

    pause 0.75

    scene v11th7 # FPP. MC and Riley are standing in front of the store, Riley has her back to the entrance, MC and Riley looking at each other, Riley slight smile, mouth closed
    with fade

    u "Is this it?"

    scene v11th7a # FPP. Same as v11th7, Riley mouth open, slight smile
    with dissolve

    ri "Yep, let's go in."

    scene v11th8 # TPP. Show MC and Riley entering the store, both slight smiles, mouths closed
    with dissolve

    pause 0.75

    scene v11th9 # TPP. Show MC and Riley walking around the store, both neutral expression, mouth closed
    with dissolve

    pause 0.75

    scene v11th99 # TPP. Show MC and Riley looking around, show Duncan in the background looking at them, moving towards them
    with dissolve

    pause 0.75

    scene v11th10 # FPP. MC is looking at Duncan, Duncan standing in front of MC, Duncan looking at Riley (Riley standing next to MC, out of shot), Duncan has a slight smile, mouth open
    with dissolve

    dun "Hello, how can I help you?"

    scene v11th11 # FPP. Same character positioning as v11th9, MC looking at Riley, Riley looking at Duncan (Duncan out of shot), Riley mouth open, slightly confused
    with dissolve

    ri "We're not too sure. We think we're looking for a man named Duncan."

    scene v11th11a # FPP. Same cam as v11th11, Riley handing out the map to Duncan, Riley mouth closed, neutral expression
    with dissolve

    pause 0.75

    scene v11th10a # FPP. Same cam as v11th10, Duncan looking at the map, mouth open, serious expression
    with dissolve

    dun "How did you two come across this map?"

    scene v11th10b # FPP. Same as v11th10a, but Duncan looking at MC, mouth closed, serious expresion
    with dissolve

    u "Our teacher gave it to us and asked us to follow it through."

    scene v11th10c # FPP. Same as v11th10b, Duncan mouth open, serious expression
    with dissolve

    dun "You do understand that this is a very serious map and those of us, such as myself that protect the clues, take it very seriously?"

    scene v11th11b # FPP. Same cam as v11th11, but Riley now looking at MC, slight smile, mouth open
    with dissolve

    ri "See [name], told you it was real."

    scene v11th10c
    with dissolve

    dun "Oh, so you're a doubter?"

    scene v11th10b
    with dissolve

    u "I-"

    scene v11th11
    with dissolve

    ri "No, he's not a doubter. So are you giving us the next riddle, or clue, or something?"

    scene v11th10
    with dissolve

    dun "Only after you complete the trial. Only the bravest of souls can be trusted with the remaining clues."

    scene v11th10b
    with dissolve

    u "What trial?"

    scene v11th10c
    with dissolve

    dun "A true test of aim. Follow me."

    scene v11th12 # TPP. Show MC, Riley and Duncan walking towards the back of the store, Duncan serious, mouth closed, MC and Riley slightly excited, mouth closed
    with dissolve

    pause 0.75

    scene v11th13 # FPP. Duncan is standing on one side of the table, MC and Riley on the other side, MC looking at Duncan, Duncan looking at MC, Duncan serious expression, mouth closed, show the table with the gun and 2 airsoft BBs on the table
    with dissolve

    pause 0.75

    scene v11th14 # FPP. Same positioning as v11th13, MC looking at Riley, Riley looking at Duncan, Riley excited, mouth open
    with dissolve

    ri "Oooh! Can I do the shooting? *Chuckles*"

    scene v11th13a # FPP. Same as v11th13, Duncan looking at Riley, Duncan mouth open, serious expression
    with dissolve

    dun "Yes, you can. In fact, you must."

    scene v11th13
    with dissolve

    u "She must?"

    scene v11th13b # FPP. Same as v11th13, Duncan serious expression, mouth open
    with dissolve

    dun "And you must as well, sir. If she misses."

    scene v11th13c # FPP. Same as v11th13, serious expression, mouth closed, Duncan messing with the gun
    with dissolve

    pause 0.75

    scene v11th13d # FPP. Same as v11th13, Duncan holding the gun, looking at MC, Duncan mouth open, serious expression
    with dissolve

    dun "I've put two bullets into this gun. One of you will shoot the gun first, attempting to get a bullseye. If that person fails to complete the trial, the other must shoot the gun."

    scene v11th13e # FPP. Same as v11th13d, Duncan looking at Riley, Duncan mouth open, serious expression
    with dissolve
    
    dun "If you both fail to get one bullseye, the treasure shall not be yours."

    scene v11th14a # FPP. Same as v11th14, Riley shocked expression, mouth open
    with dissolve

    ri "What?! You're seriously saying that we only get one shot at this?"

    scene v11th13e
    with dissolve

    dun "Well, two shots. One for each of you."

    scene v11th14a
    with dissolve

    ri "How is it even possible for us to do that without having any practice or experience or-"

    scene v11th14b # FPP. Same as v11th14, Riley looking at MC, Riley shocked expression, mouth closed
    with dissolve

    u "It'll be fine, Riley. We'll both take our best shot and if we don't win, well... who cares anyway? This whole thing is probably some dumb prank they pull on tourists."

    scene v11th13f # FPP. Same as v11th13e, Duncan slightly annoyed, mouth open
    with dissolve

    dun "\"Some dumb prank\", sir?"

    scene v11th14c # FPP. Same as v11th14a, Riley slight smile, mouth open
    with dissolve

    ri "*Chuckles* Well... Whatever it is, tradition or not. We'll be finding out soon enough."

    scene v11th15 # TPP. Show Riley grabbing the gun from Duncan, Duncan slightly shocked, mouth closed, Riley excited, mouth closed
    with dissolve

    pause 0.75

    scene v11th13g # FPP. Same as v11th13e, Duncan without the gun, looking at Riley, Duncan shocked, mouth open
    with dissolve

    dun "Wait! You both must agree on who takes the first shot."

    scene v11th13h # FPP. Same as v11th13d, Duncan without the gun, Duncan slight smirk, mouth closed, different pose
    with dissolve

    u "Why does it matter who goes first if we're both gonna have to shoot the gun anyway?"

    scene v11th13i # FPP. Same as v11th13h, Duncan slight smirk, mouth open
    with dissolve

    dun "Ah, so you must think that if your friend here goes first, you'll have to shoot the gun anyway, because she'll miss the shot?"

    scene v11th13h
    with dissolve

    menu:
        "She will miss":
            $ v11_she_will_miss = True
            $ reputation.add_point(RepComponent.TROUBLEMAKER)
            scene v11th14d # FPP. Same as v11th14, MC and Riley looking at each other, Riley holding the gun, Riley mouth closed, slight smirk
            with dissolve

            u "Those are my expectations, yeah. Sorry Riley."

            scene v11th14e # FPP. Same as v11th14d, Riley mouth open, slight smirk
            with dissolve

            ri "Okay, [name]. *Chuckles* If you're so confident about that, you go first."

            scene v11th14f # FPP. Same as v11th14d, Riley giving the gun to MC, Riley mouth closed, slight smirk
            with dissolve

            u "(Fuck...)"

            scene v11th16 # TPP. Show MC walking towards the range while holding the gun, MC mouth closed, nervous expression
            with dissolve

            pause 0.75

            scene v11th17 # TPP. MC standing in front of the bullseye, looking at it, holding the gun (not aiming yet), MC mouth open, nervous expression
            with dissolve
            
            u "Okay... I just need to focus..."

            scene v11th17a # TPP. Same as v11th17, MC now aiming the gun at the bullseye, mouth slightly open, nervous expression
            with dissolve

            u "Three... Two... One..."

            play sound sound.gun

            pause 0.5

            scene v11th18 # TPP. Show a bullet mark that nearly missed the bullseye
            with dissolve

            pause 1

            scene v11th19 # FPP. Riley standing next to MC, MC standing same place as v11th17, MC and Riley looking at each other, Riley slight smile, mouth closed
            with dissolve

            u "Oh, shit... I'm sorry Riley, I-"

            scene v11th19a # FPP. Same as v11th19, Riley slight smile, mouth open
            with dissolve

            ri "*Laughs* It's fine, [name]. But maybe you could be a little less cocky next time? *Chuckles*"

            scene v11th20 # TPP. Show Riley taking the gun from MC, MC slightly sad, mouth closed, Riley slightly nervous, mouth closed
            with dissolve

            pause 0.75

            scene v11th17b # TPP. Same camera as v11th17, Riley standing in front of the bullseye, looking at it, holding the gun (not aiming yet), Riley mouth closed, nervous expression 
            with dissolve

            pause 0.75

            scene v11th17c # TPP. Same as v11th17b, Riley now aiming the gun at the bullseye, mouth closed, nervous expression
            with dissolve

            play sound sound.gun
            pause 1

            scene v11th18a # TPP. Show a bullet mark that hit right in the middle of the bullseye
            with dissolve

            pause 1

            scene v11th17d # TPP. Same as v11th17b, Riley not aiming, very shocked, mouth closed
            with dissolve

            u "(No fucking way...)"

            scene v11th17e # TPP. Same as v11th17d, Riley mouth open, very excited
            with dissolve

            ri "DID I JUST GET A BULLSEYE?!"

            scene v11th21 # TPP. Show MC and Riley hugging, same position as v11th17b, both very excited, mouths closed
            with dissolve

            pause 1.25

            scene v11th22 # FPP. Same positioning as v11th21, MC and Riley no longer hugging, looking at each other, Riley very excited, mouth closed
            with dissolve

            u "YES! You just got a fucking bullseye, Riley! How the hell did you do that?"

            scene v11th22a # FPP. Same as v11th22, Riley very excited, mouth open
            with dissolve

            ri "I guess it just comes naturally... *Chuckles*"

        "She won't miss":
            $ reputation.add_point(RepComponent.BRO)
            scene v11th14g # FPP. Same as v11th14d, Riley smiling, mouth closed, different pose
            with dissolve

            u "Actually, I'm pretty confident she's got this. *Chuckles*"

            scene v11th14h # FPP. Same as v11th14g, Riley smiling, mouth open
            with dissolve

            ri "Really?! Thanks, [name]. *Chuckles* I won't let you down."

            scene v11th16a # TPP. Same as v11th16, but Riley instead of MC
            with dissolve

            pause 0.75

            scene v11th17b 
            with dissolve

            pause 0.75

            scene v11th17c
            with dissolve

            play sound sound.gun

            pause 1

            scene v11th18
            with dissolve

            pause 1

            scene v11th22b # FPP. Same as v11th22, Riley sad, mouth open, different pose, holding the gun
            with dissolve

            stop sound

            ri "Oh no... I'm so sorry! You have to get a bullseye, [name]. This is our only chance!"

            scene v11th22c # FPP. Same as v11th22b, Riley sad, mouth closed, handing the gun to MC
            with dissolve

            pause 0.75

            scene v11th17
            with dissolve

            u "(Fuck...) It's okay... I just need to focus..."

            scene v11th17a
            with dissolve

            u "Three... Two... One..."

            play sound sound.gun

            pause 0.75

            scene v11th18a
            with dissolve

            grant achievement("on_target", "Hit a bullseye at Duncan's")

            stop sound

            pause 2

            scene v11th21a # TPP. Same as v11th21, but MC and Riley inverted positions
            with dissolve

            pause 1

            scene v11th23 # FPP. Same positioning as v11th21a, MC and Riley no longer hugging, looking at each other, Riley mouth open, very excited
            with dissolve

            ri "HOLY SHIT! You just fucking did that, [name]! That was amazing!"

            scene v11th23a # FPP. Same as v11th23, Riley very excited, mouth closed
            with dissolve

            u "I can't believe that actually just happened..."
        
    scene v11th16b # TPP. Same as v11th16, but MC and Riley now shown walkign towards the table, both very excited, mouths closed
    with dissolve

    pause 0.75

    scene v11th13j # FPP. Same as v11th13i, Duncan slightly annoyed, mouth open, different pose
    with dissolve

    dun "Well, what do you know? You two must be the chosen ones... Hmmph."

    scene v11th12a # TPP. Same as v11th12, but they're walking towards the store counter
    with dissolve

    pause 0.75

    scene v11th24 # TPP. Show Duncan headed for the counter, MC and Riley kind of far behind him, Duncan slightly annoyed, mouth closed, Riley and MC smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11th25a # FPP. Same positioning as v11th24, MC and Riley looking at each other, Riley slight smile, mouth closed
    with dissolve

    u "*Whispers* \"The chosen ones\"? What's going on here, Riley? *Chuckles*"

    scene v11th25 # FPP. Same as v11th25, Riley slight smile, mouth slightly open
    with dissolve

    ri "*Whispers* Haha, hush! *Chuckles* Let's just get the next part of the riddle and get out of here."

    scene v11th26 # TPP. Show Riley and MC going to the counter, Duncan already on the other side of it, Riley and MC smiling, Duncan slightly annoyed, mouths closed
    with dissolve

    pause 0.75

    scene v11th27 # TPP. Same positioning as v11th26, MC and Riley now standing opposite to Duncan, Duncan handing out a piece of the map, Riley grabbing it, Duncan serious expression, MC and Riley smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11th28 # FPP. Same positioning as v11th27, MC and Duncan looking at each other, Duncan serious expression, mouth open
    with dissolve

    dun "This treasure cannot be found by anyone other than yourselves. Do you understand?"

    scene v11th28a # FPP. Same as v11th28, Duncan mouth closed, slightly annoyed, different pose
    with dissolve

    u "*Chuckles*"

    scene v11th29 # FPP. Same positioning as v11th27, MC and Riley looking at each other, Riley slightly stern face, mouth open
    with dissolve

    ri "Ahem! Excuse you, [name]..."

    scene v11th29a # FPP. Same as v11th29, Riley looking at Duncan, Riley slight smile, mouth open
    with dissolve
    
    ri "Yes, we understand. Thank you, Mr... Duncan."

    scene v11th27a # Same as v11th27, Riley pulling MC by the hand, Riley smiling, mouth closed, MC slightly smiling, slightly unbalanced, mouth closed
    with dissolve

    pause 0.75
    stop music fadeout 3
    play music music.ck1.v11.Track_Scene_20_2 fadein 2
    scene v11th30 # TPP. Show MC and Riley leaving the store, both mouths closed, smiling
    with dissolve

    pause 0.75

    scene v11th4a # TPP. Same as v11th4, but they're walking to the opposite direction, both smiling, mouths closed
    with dissolve

    pause 0.75

    scene v11th3a # TPP. Same as v11th3, but they're walking into the hotel lobby, show Mr Lee and Nora talking in the background
    with dissolve

    pause 0.75

    scene v11th31 # FPP. MC and Riley standing in the hotel lobby, looking at each other, Riley smiling, mouth open
    with dissolve

    ri "Ahh, home sweet home. *Chuckles*"

    scene v11th31a # FPP. Same as v11th31, Riley mouth closed, smiling
    with dissolve

    u "Is it cool if we take a break from the weird trials for today? *Laughs*"

    scene v11th31b # Same as v11th31, Riley mouth open, smiling, Mr Lee is now approaching them (mouth closed, slightly smiling)
    with dissolve

    ri "Haha, yes, of course."

    scene v11th32 # FPP. Mr Lee is now standing next to Riley (Riley out of shot), Mr Lee looking at Riley, mouth open, slight smile
    with dissolve

    lee "Riley, [name], where were you two this early?"

    scene v11th31c # FPP. Same as v11th31, Riley is now looking at Mr Lee (Mr Lee same position as v11th32). Riley mouth open, smirking
    with dissolve

    ri "Just practicing our aim..."

    scene v11th32a # FPP. Same as v11th32, Mr Lee looking at MC, different pose, Mr Lee slight smile, mouth open
    with dissolve

    lee "I see. Well, you're just in time. We're going to the museum and while the other students are touring with Ms. Rose, you all will be with me."

    jump v11_going_to_museum