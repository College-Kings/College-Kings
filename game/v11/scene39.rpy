# SCENE 39: Gokarting
# Location: Streets, Gokarting place, hotel lobby
# Characters: MC (Outfit 9), Amber (Outfit 1), Clerk (Outfit ask lew)
# Time: Afternoon
label v11_mc_amber_gokart:
    scene v11cam1 # TPP. MC and amber in the streets
    with dissolve
    play music "music/v10/Track Scene 9.mp3" fadein 2
    pause 0.75

    scene v11cam2a # FPP. MC looks at amber, mouth opened (walking)
    with dissolve

    am "Before we get in here, let's make a bet."

    scene v11cam2 # FPP. Same as 2, mouth closed
    with dissolve

    u "A bet?"

    scene v11cam2a
    with dissolve

    am "Yeah, a bet. I want something if I win."

    scene v11cam2
    with dissolve

    u "But what if you lose?"

    scene v11cam2a
    with dissolve

    am "Then I guess that means you get something."

    scene v11cam2
    with dissolve

    u "Okay... What are you thinking?"

    scene v11cam2a
    with dissolve

    am "Let's do this - whoever loses has to post a picture of themselves near the Eiffel Tower on Kiwii."

    scene v11cam2
    with dissolve

    u "Okay? That seems easy enough. *Chuckles*"

    scene v11cam2b # FPP. same as 2, slight smile on her face, mouth opened
    with dissolve

    am "Completely nude."

    scene v11cam2
    with dissolve

    u "What? That's crazy!"

    scene v11cam2c # FPP. Same as 2, smile on her face, mouth opened
    with dissolve

    am "*Laughs* You don't have to leave it up long. Just like ten minutes. Then you can delete it."

    scene v11cam2
    with dissolve

    u "And when you lose, you're gonna be okay doing that?"

    scene v11cam2c
    with dissolve

    am "Haha, yeah. I won't be losing, but... yeah. *Chuckles*"

    scene v11cam2
    with dissolve

    u "*Sighs* You're brave, Amber. I'll give you that, but I'm telling you right now... You're going to regret this."

    scene v11cam2b
    with dissolve

    am "Oh [name]... I highly doubt that. *Chuckles*"

    scene v11cam3 # TPP. MC and amber walking into the go kart place
    with dissolve

    pause 0.75

    scene v11cam4 # FPP. now inside of the kart place, looking at amber mouth closed
    with dissolve

    u "So, have you done this before?"

    scene v11cam4a # FPP. Same as 4, mouth closed
    with dissolve

    am "Nope, what about you?"

    scene v11cam4
    with dissolve

    u "I can't remember... Maybe once when I was younger."

    scene v11cam4b # FPP. Same as 4, smile on her face, mouth opened
    with dissolve

    am "Mhmm, right... *Chuckles*"

    scene v11cam5 # TPP. MC and amber meet the track clerk on the track
    with dissolve

    pause 0.75 

    scene v11cam6 # FPP. Now in a little room with the clerk, clerks mouth opened
    with dissolve

    clerk "I'm sorry, but if you're looking to ride, we're closed."

    scene v11cam6a # FPP. Same as 6, clerks mouth closed ambers mouth opened and amber looks annoyed
    with dissolve

    am "What?! You're really closed?"

    scene v11cam6b # FPP. The clerk stares at amber with awe, mouth opened
    with dissolve

    clerk "I, uh..."
    
    scene v11cam6a 
    with dissolve

    am "Uhh, hello?"

    scene v11cam6b
    with dissolve

    clerk "My b-bad, uhm, s-sorry you're just really... You two aren't... dating, are you?"
   
    scene v11cam6c # FPP. same as 6a, amber is now smiling
    with dissolve

    u "*Chuckles* What?"

    scene v11cam6b
    with dissolve

    clerk "Like, um... Are you guys a couple?"

    scene v11cam6c 
    with dissolve

    am "No, why? *Chuckles*"

    scene v11cam6d # FPP. The clerk scratches his head, mouth closed
    with dissolve

    pause 0.75

    scene v11cam6c 
    with dissolve

    clerk "Oh, I was just wondering. That's good. So you're um, single then?"

    scene v11cam6a
    with dissolve

    am "Yes... Why?"

    scene v11cam6c 
    with dissolve

    clerk "Oh, no reason... Just curious. But um, yeah, the track is closed. Although, maybe for a fine dime like you I'll get it up and running..."

    scene v11cam6c
    with dissolve

    am "*Chuckles* Well, that'd be great. Thank you."

    scene v11cam6c 
    with dissolve

    clerk "Sure thing, I'll be right back."
    
    scene v11cam6e # FPP. The clerk walks off
    with dissolve

    pause 0.75

    scene v11cam7 # FPP. now looking at amber, mouth closed and smiling (angle change from 6)
    with dissolve

    u "\"For a fine dime like you...\" What a cornball. *Laughs*"

    scene v11cam7a # FPP. same as 7, amber smiling and mouth opened
    with dissolve

    am "Haha! Hey. He can be whatever he wants as long as we get to use those karts, plus it didn't sound like he was gonna make us pay."

    scene v11cam7
    with dissolve

    u "I guess being pretty pays after all, huh?"

    scene v11cam7a
    with dissolve

    am "Oh... So, that's why you're broke? *Laughs*"

    scene v11cam7
    with dissolve

    u "Ha ha, very funny."

    scene v11cam7a
    with dissolve

    am "Now listen, [name]... Don't even try to chicken out when you lose, I know how you are. *Chuckles*"

    scene v11cam7
    with dissolve

    u "What's that supposed to mean? I've never chickened out of anything..."

    scene v11cam7a
    with dissolve

    am "Mhmm, if you say so..."

    scene v11cam7
    with dissolve

    u "Ha, okay... I feel like you just want to see me naked."

    scene v11cam7a
    with dissolve

    am "And? Maybe I do... Or maybe I just want you to be embarrassed in front of everyone we know."

    scene v11cam7
    with dissolve

    u "Come on, you definitely don't want that. *Chuckles*  Wait, is it gonna be like a selfie or what?"

    scene v11cam7a
    with dissolve

    am "Haha, I hadn't even thought about that. How about when I win, I get to take the picture myself and I'll send it to you. That way if you don't post it, I will."

    scene v11cam7
    with dissolve

    u "Wow. This bet just got way more serious than I thought it'd be."

    scene v11cam7a
    with dissolve

    am "If it wasn't a serious bet there'd be no point in having it. But don't worry, if I lose you get to take my picture too. Same rules for both of us."

    menu:
        "Say nothing":
            scene v11cam7
            with dissolve

            u "..."

            scene v11cam7a
            with dissolve

            am "Weren't expecting that, huh? *Chuckles*"

            scene v11cam7
            with dissolve

            u "No... not really. Haha..."

            scene v11cam7a
            with dissolve

            am "*Laughs*"


        "Be excited":
            $ add_point(KCT.BRO)
            scene v11cam7
            with dissolve

            u "*Chuckles* Now that's the motivation I needed."

            scene v11cam7a
            with dissolve

            am "Haha, good. Now you're excited?"

            scene v11cam7
            with dissolve

            u "You've seen yourself. How could I not be? *Chuckles*"

            scene v11cam7a
            with dissolve

            am "*Laughs*"

    scene v11cam7b # FPP. The clerk walks in holding uniforms
    with dissolve

    pause 0.75

    scene v11cam8 # FPP. angle change from 7, clerks mouth opened
    with dissolve

    clerk "Now that's a laugh I could get used to! My ex wife's laugh annoyed me so damn much, I almost wished I was deaf sometimes."

    scene v11cam8a # FPP. Clerks mouth closed, amber looking weirded out, mouth opened
    with dissolve

    am "Oh... Um, okay. *Chuckles*"

    scene v11cam8b # FPP. Clerk looking creepy, same as 8
    with dissolve

    clerk "I'm not like- crazy or anything, okay? You don't think I'm crazy, do you? It's really not cool to assume things about people."
    
    scene v11cam8a 
    with dissolve

    am "N-no! You're alright dude, I don't think you're crazy."

    scene v11cam8b 
    with dissolve

    clerk "Okay good... I could've killed the last person that called me crazy. I didn't kill him but, I could've you know?"
    
    scene v11cam8a
    with dissolve

    am "Yeah, for sure."

    scene v11cam8c # FPP. clerk is now smiling, clerks mouth opened
    with dissolve

    clerk "Anyway, here you go."

    scene v11cam9 # TPP. The clerk hands them both a uniform
    with dissolve

    pause 0.75

    scene v11cam8a
    with dissolve

    clerk "Wear these. You can just put them over the clothes you're wearing. They're for extra protection in the event of a crash."
    
    scene v11cam8d # FPP. Looking at the clerk, mouth closed
    with dissolve

    u "A crash? How often does that happen?"

    scene v11cam8b 
    with dissolve

    clerk "A lot."
    
    scene v11cam8e # FPP. looking at amber smiling, mouth opened
    with dissolve

    am "Don't try backing out now, [name]. *Chuckles*"

    scene v11cam8b
    with dissolve

    clerk "Yeah [name], don't be scared..."
    
    scene v11cam8d 
    with dissolve

    u "(What the fuck is wrong with this guy?)"

    scene v11cam8f # FPP. looking at amber, mouth closed
    with dissolve

    u "Haha, let's do this."

    scene v11cam10 # TPP. MC and amber leaving the room and walking towards the karts
    with dissolve

    pause 0.75

    scene v11cam11 # FPP. looking at amber, smile on her face, mouth opened
    with dissolve

    am "That guy is fucking crazy. *Laughs*"

    scene v11cam11a # FPP. same as 11, mouth closed
    with dissolve

    u "Really? I hadn't noticed..."

    scene v11cam11
    with dissolve

    am "*Laughs*"

    scene v11cam12 # TPP. MC and amber place their uniforms
    with dissolve

    pause 0.75

    scene v11cam13 # TPP. MC and amber are standing next to their karts stationed next to each other
    with dissolve

    pause 0.75

    scene v11cam14 # FPP. looking at amber, smile on her face, mouth opened
    with dissolve

    am "Haha, we look like we're getting ready to rob a bank. I'm pretty sure we could if we really wanted to, especially here in another country, as students on a trip. No one would suspect us, you know that... right?"
    am "I wonder who else we could get in on it... maybe Riley. *Chuckles* If anything went south we could use her as the fall guy. She's hot and all, but definitely the perfect fall guy."

    menu:
        "Agree":
            $ add_point(KCT.TROUBLEMAKER)
            scene v11cam14a # FPP. looking at amber, mouth closed
            with dissolve

            u "Sounds like an interesting plan, but you have to promise that if we're successful, we'll visit Riley in prison until the day she gets out."

            scene v11cam14b # FPP. Same as 14a, mouth opened
            with dissolve

            am "Sorry [name], but that's not gonna happen. It's way too risky visiting the person who takes the fall. Can't even do calls, actually. The most I can do is fake cry during any interviews."

            scene v11cam14a 
            with dissolve

            u "That's cold..."

            scene v11cam14b
            with dissolve

            am "We all have to play a role, and if you can't play yours then unfortunately you and Riley will both be doing ten to twenty years in the slammer."

            scene v11cam14a
            with dissolve

            u "*Chuckles* Wow Amber, you've really thought this whole thing through, huh?"

            scene v11cam14b
            with dissolve

            am "Just a little in my spare time. *Chuckles* Let's do this race."

            scene v11cam14a
            with dissolve

        "Disagree":
            $ add_point(KCT.BOYFRIEND)
            scene v11cam14c # FPP. Same as 14, mouth closed
            with dissolve

            u "Nah, I'm good. I'm way too handsome for jail."

            scene v11cam14b
            with dissolve

            am "\"Nice to meet you guys, my name's [name] and I didn't see myself going to prison, but I'm gonna do whatever I gotta do to make it.\"" 
            am "\"*Mimicking prisoner* Cool, and my name's Bruce, now get naked before I come over there and take it!\""

            scene v11cam14a
            with dissolve

            u "Oh you a freak, freak."

            scene v11cam14b
            with dissolve

            am "\"No, but you finna take this meat meat.\""

            scene v11cam14c
            with dissolve

            u "Okay... Please tell me we're done with this conversation. *Chuckles*"

            scene v11cam14
            with dissolve

            am "Haha, fine. Let's do this race."

    play music "music/v10/Track Scene 24_1.mp3" fadein 2
    scene v11cam15 # TPP. MC and amber hop in their karts, helmets on
    with dissolve

    pause 0.75

    scene v11cam16 # FPP. MC and Amber sitting in their karts, looking at each other, Amber neutral expression, helmet on
    with dissolve

    u "You ready over there?"

    scene v11cam16a # FPP. Same as v11cam16, Amber head slightly tilted to indicate she~s talking (mouth doesn't show with this helmet)
    with dissolve

    am "Don't worry about me, [name]!"

    scene v11cam17 # FPP. MC look as the clerk walks in front of the karts, slight smile, mouth closed 
    with dissolve

    pause 0.75

    scene v11cam17a # FPP. Same as v11cam17, looking at MC, mouth opened
    with dissolve

    clerk "Alright, I'll be giving you guys a countdown. It's a three lap race, so you'll start and finish here. And you-"
    
    scene v11cam17b # FPP. Same as v11cam17a, mouth closed
    with dissolve

    u "Me?"

    scene v11cam17a 
    with dissolve

    clerk "Yeah, you. Your name must have not been interesting enough for me to remember... Anyways, I have a message for you from the gun show... Don't cheat this beautiful lady out of a win."
    
    scene v11cam16a 
    with dissolve

    am "Yeah [name], don't cheat. *Chuckles*"

    scene v11cam16
    with dissolve

    u "Whatever..."

    scene v11cam17b
    with dissolve

    u "(Fucking weirdo, this guy.)"

    scene v11cam17c # FPP. MC looking at clerk, clerk looking at Amber, Clerk mouth open, slight smile
    with dissolve

    clerk "Alright... May the cute one win! Three... two... one... GO!"
    
    scene v11cam18 # FPP. Amber kart in front of mc's kart (Location 1 - Close to the starting line)
    with dissolve

    u "WHAT?! WHY IS YOURS SO FAST?"

    scene v11cam19 # TPP. Show the clerk looking at MC, slight grin, mouth open, Clerk is standing to the side, watching the race
    with dissolve

    clerk "You can thank me for that."
    
    scene v11cam20 # FPP. MC closer to Amber's kart now (Location 2)
    with dissolve

    pause 0.75

    scene v11cam20a # FPP. Same as v11cam20, MC right behind amber, Amber looking back at MC, slightly scared (Location 2)
    with dissolve

    am "Huh? Wait! Why am I slowing down?"

    menu (fail_label="v11_no_overtake1"):
        "Overtake Amber":
            $ v11_overtake_points += 1

            scene v11cam21 # FPP. MC Overtaking amber (location 2)
            with dissolve

            pause 0.75

            scene v11cam22 # FPP. MC looking back at amber, pretty far from MC (location 2)
            with dissolve

            u "SEE YOU AT THE FINISH LINE! *Laughs*"

            scene v11cam22a # FPP. Same as v11cam22, MC looking back at Amber, Amber angry, a bit closer to MC (location 2)
            with dissolve

            am "Ugh, don't get cocky just yet, dickhead!"

            scene v11cam23 # TPP. Amber overtaking MC (location 3)
            with dissolve

            pause 0.75

            scene v11cam24 # FPP. Amber ahead of MC, looking back at him (Location 3)
            with dissolve

            u "You couldn't have given me at least a few seconds in the lead?"

            scene v11cam24a # FPP. Same as v11cam24, Amber looking back at MC, even further ahead, smiling (Location 3)
            with dissolve

            am "I don't like giving people false hope. *Chuckles*"

            jump v11_race_continue1

        "See what's wrong":
            jump v11_no_overtake1

label v11_no_overtake1:
    scene v11cam25 # FPP. MC driving next to amber, looking at her (location 2)
    with dissolve

    u "You okay over here? Is it messing up or something?"

    scene v11cam25a # FPP. Same as v11cam25, Amber leans over throwing dirt on MCs face, make screen blurry for realism
    with dissolve

    u "What the fuck did you just throw in my face?!"

    scene v11cam25b # FPP. Same as v11cam25, Now the screen is blurry, show amber moving her head as if she was laughing, driving more in front of MC now 
    with dissolve

    am "A distraction! *Laughs*"

    scene v11cam24b # FPP. Same as v11cam24, Amber looking forward
    with dissolve

    pause 0.75

    jump v11_race_continue1

label v11_race_continue1:
    scene v11cam19a # FPP. Same as v11cam19, Clerk looking at Amber, Clerk screaming, watching the race, mouth opened, excited (looking at location 4)
    with dissolve

    clerk "That's my girl!"

    scene v11cam26 # FPP. Amber in front of mc, looking back at him (location 4)
    with dissolve

    u "So you think you can play cold, huh?"

    scene v11cam26a # FPP. Same as v11cam26, Amber moving her head as if she was laughing (location 4)
    with dissolve

    am "Mhmm, ice in these veins, baby. *Chuckles*"

    scene v11cam27 # FPP. Show MC driving behind Amber (location 5)
    with dissolve

    pause 0.75

    scene v11cam28 # FPP. Show MC driving behind Amber (Location 6)
    with dissolve

    pause 0.75

    scene v11cam29 # FPP. MC watches as Amber crosses the finish line, Amber looks back at MC (Location 1)
    with dissolve

    am "THAT'S A LAP! HOPE THE WORLD LIKES SEEING YOU NAKED! *Chuckles*"

    scene v11cam19b # FPP. Same as v11cam19, Clerk looking at Amber, he's smiling, mouth open (looking towards location 1)
    with dissolve

    clerk "Wait, did you say you want to see me naked?"

    scene v11cam25 
    with dissolve

    u "I think your boyfriend's talking to you, Amber!"

    scene v11cam25c # FPP. Same as v11cam25, Amber now slightly ahead of MC, looking back at him 
    with dissolve

    am "Distractions don't work on me, but nice try."

    scene v11cam26b # FPP. Same as v11cam26, Amber looking straight ahead
    with dissolve

    u "(If I do distract her though, I could take the lead.)"

    scene v11cam27
    with dissolve

    menu (fail_label="v11_no_overtake2"):
        "Riley's here":
            $ v11_overtake_points += 1

            scene v11cam27a # FPP. Same as v11cam27, Amber looking back at MC
            with dissolve

            u "Oh, hey! Looks like we have company!"

            scene v11cam27b # FPP. Same as v11cam27, MC's kart closer to Amber, looking at MC
            with dissolve

            am "What?"

            scene v11cam27c # FPP. Same as v11cam27b, Amber looking towards the entrance of the go karting place
            with dissolve

            u "Look, Riley's here!"

            scene v11cam27b
            with dissolve

            am "Where?"

            scene v11cam30 # TPP. Amber looks away from the track trying to find riley, MC overtakes her (Location 6)
            with dissolve

            pause 0.75

            scene v11cam31 # FPP. MC's kart is in front of Amber's, he is looking back at her, she's angry (Location 7)
            with dissolve

            u "Wow, never took you for such a gullible person."

            scene v11cam31a # FPP. Same as v11cam31, Amber's kart is closer now
            with dissolve

            am "I'm not gullible, jackass."

            scene v11cam31b # FPP. Same as v11cam31a, Amber's kart is closer to MC
            with dissolve

            u "Oh, you must just really like Riley then..."

            scene v11cam31c # FPP. Same as v11cam31b, Amber looking away from MC, avoiding eye contact
            with dissolve

            am "..."

            scene v11cam31b
            with dissolve

            u "Oh. And the secret's out. *Laughs*"

            scene v11cam31d # FPP. Same as v11cam31b, Amber's kart even closer
            with dissolve

            am "The only secret coming out is gonna be your junk on the front page of Kiwii."

            scene v11cam32 # FPP. MC looking as Amber overtakes him, Amber looking straight ahead (Location 8)
            with dissolve

            u "Dude, how do you keep passing me!?"

            scene v11cam32a # FPP. Same as v11cam32, Amber further ahead than MC, she's looking back at him 
            with dissolve

            am "Cause I got the juice! *Laughs*"

            jump v11_race_continue2

        "There's something in your hair":       
            jump v11_no_overtake2

label v11_no_overtake2:
    scene v11cam27a
    with dissolve

    u "Hey!"

    scene v11cam27b
    with dissolve

    am "What?!"

    scene v11cam33 # FPP. Amber's kart in front of MC's, Amber looking back at him (Location 6)
    with dissolve

    u "There's something in your hair!"

    scene v11cam33a # FPP. Same as v11cam33, Amber's kart even further away
    with dissolve

    am "Wow [name], you really thought that would work? *Chuckles*"

    scene v11cam32a
    with dissolve

    u "(Fuck.) Sort of. *Chuckles*"

    jump v11_race_continue2

label v11_race_continue2:
    scene v11cam29
    with dissolve

    am "THAT'S LAP TWO! AND LOOK WHO'S IN THE LEAD, STARTING TO SEE A PATTERN HERE!"

    scene v11cam29a # FPP. Same as v11cam29, Amber's kart further ahead than MC's
    with dissolve

    u "Only lap that matters is the last one!"

    scene v11cam20b # FPP. Same as v11cam20, Amber looking back at MC, laughing
    with dissolve

    am "Right. Same shit, different lap. *Chuckles*"

    scene v11cam20a
    with dissolve

    u "This race really doesn't feel fair, feels like you're playing me dirty."

    scene v11cam24
    with dissolve

    am "That's the only way I know how..."

    scene v11cam19a 
    with dissolve

    clerk "Last lap baby, you got his ass!"

    scene v11cam27a
    with dissolve

    u "You ever gonna respond to this guy? Maybe he'll take you out after the race. *Chuckles*"

    scene v11cam27b
    with dissolve

    am "*Chuckles* Don't make me laugh [name], I'm too busy winning."

    scene v11cam33
    with dissolve

    u "That's what you think!"

    menu (fail_label="v11_no_overtake3"):
        "Sharp turn":
            $ v11_overtake_points += 1

            scene v11cam34 # TPP. Show MC's kart drifting in front of Amber's through a corner (Corner between Location 6 and 7)
            with dissolve

            pause 0.75

            scene v11cam31
            with dissolve

            am "WOAH! You got a little close there, crazy."

            scene v11cam31a
            with dissolve

            u "Gotta do what I gotta do to win."

            scene v11cam31b
            with dissolve

            am "Haha, I see."

            scene v11cam35 # FPP. MC looks back and sees Amber very close to him, MC is slightly in front (Location 8)
            with dissolve

            pause 0.75

            jump v11_race_continue3

        "Steady":
            jump v11_no_overtake3

label v11_no_overtake3:
    scene v11cam34a # TPP. Same as v11cam24, but Amber's kart is turning in front of MC's kart
    with dissolve

    pause 0.75

    u "(Nothing drastic, I shouldn't be taking risks right now.)"

    scene v11cam36 # FPP. Amber's kart is slightly in front of MC's, she is looking back at MC (Location 7)
    with dissolve

    am "It sure is lonely way up here, wish I had some company."

    scene v11cam36a # FPP. Same as v11cam36, but Amber's kart is even further ahead
    with dissolve

    u "Be careful what you wish for!"

    scene v11cam37 # FPP. Amber looking straight ahead, MC very close to Amber's kart, MC is slightly behind (Location 8)
    with dissolve

    pause 0.75

    jump v11_race_continue3

label v11_race_continue3:
    scene v11cam38 # FPP. Amber's kart is slightly ahead of MC's, she's looking back at him (Location 9 - right after the final corner before finish line)
    with dissolve

    am "This is it right here."

    scene v11cam38a # FPP. Same as v11cam38, Amber's kart slightly less ahead
    with dissolve

    u "*Laughs*"

    scene v11cam39 # TPP. Show MC's kart slightly ahead of Amber's (Slightly before the finish line)
    with dissolve

    pause 0.75

    scene v11cam40 # TPP. Show Amber's kart ahead of MC's (Slighty in front of v11cam39 location)
    with dissolve

    pause 0.75

    if v11_overtake_points >= 2:
        scene v11cam41 # TPP. Show MC's kart passing the finish line first, with Amber slightly behind him
        with dissolve

        pause 0.75

        scene v11cam16
        with dissolve

        u "LET'S GOOOOOOO!!!!"

        scene v11cam15a # TPP. Same camera as v11cam15, Show MC and Amber getting out of their karts
        with dissolve

        pause 0.75

        scene v11cam14d # FPP. Same as v11cam14, Amber slightly annoyed, mouth closed
        with dissolve

        u "Looks like somebody's gonna be an erotic Kiwii model!"

        scene v11cam14e # FPP. Same as v11cam14d, mouth open
        with dissolve

        am "Haha, I like that you're excited but slow your roll, hotshot."

        scene v11cam14d
        with dissolve

        u "Don't tell me you're the chicken now."

        scene v11cam14e
        with dissolve

        am "No, definitely not."

        scene v11cam42 # FPP. Same positioning as v11cam14, MC looks as the clerk walks towards them, Clerk slightly annoyed, mouth closed
        with dissolve

        pause 0.75

        scene v11cam42a # FPP. Same as v11cam42, Clerk nos standing close Amber, Clerk looking towards Amber, Clerk slight smirk, mouth open
        with dissolve

        clerk "Sorry about that babe, we both know this guys a cheater. I'll take care of him for you."

    else:
        scene v11cam41a # TPP. Same as v11cam41, but Amber's kart passes the finish line first
        with dissolve

        pause 0.75

        scene v11cam16a
        with dissolve

        am "Haha, Easy!"

        scene v11cam15a
        with dissolve

        u "*Sighs*"

        scene v11cam14b
        with dissolve

        am "Aww, you mad? *Chuckles*"

        scene v11cam14a
        with dissolve

        u "What? No."

        scene v11cam14b
        with dissolve

        am "Why'd you sigh like that then?"

        scene v11cam14a
        with dissolve

        u "Just thinking about what's gonna happen when everyone sees me naked on Kiwii... it's gonna be bad. *Chuckles*"

        scene v11cam14b
        with dissolve

        am "Hey, I could be doing you a favor. Who knows, someone may like what they see. *Chuckles*"

        scene v11cam14a
        with dissolve

        u "Haha yeah... We'll see how it goes."

        scene v11cam42a
        with dissolve

        clerk "Hey babe, nice job! Have fun?"
    stop music fadeout 3
    play music "music/v10/Track Scene 9.mp3" fadein 2
    scene v11cam14f # FPP. Same as v11cam14, Amber looking towards the clerk (Clerk positioned like v11cam42a), Amber annoyed, mouth open
    with dissolve

    am "Look weirdo, thanks for letting us race, but don't ever call me babe again. Alright?"

    scene v11cam42c # FPP. Same as v11cam42a, Clerk annoyed, mouth open
    with dissolve

    clerk "Babe? I didn't call you babe! I don't even like girls like you, you're definitely not my type. You're like a six if that. *Laughs*"

    scene v11cam14g # FPP. Same as v11cam14f, Amber mouth closed
    with dissolve

    menu:
        "Stand up for Amber":
            $ add_point(KCT.BOYFRIEND)
            $ add_point(KCT.TROUBLEMAKER)
            $ amber.points += 1

            scene v11cam14f
            with dissolve

            am "I'm-"

            scene v11cam42d # FPP. Same as v11cam42a, Clerk looking at MC, Clerk annoyed, mouth closed
            with dissolve

            u "What the fuck is your problem? Who do you think you are talking to her like that? Your ugly ass probably never pleased a girl in his life."

            scene v11cam42e # FPP. Same as v11cam42d, Clerk mouth open
            with dissolve

            clerk "I didn't mean it like that, I was just saying-"

            scene v11cam42d
            with dissolve

            u "Don't \"just say\" anything. Let's go, Amber."

            scene v11cam43 # TPP. Show MC walking towards the entrance, pulling Amber by the hand, Amber not moving, looking surprised, mouth closed, MC angry, mouth closed
            with dissolve

            pause 0.75

            scene v11cam99 # FPP. Same positioning as v11cam43, MC holding Amber's hand, looking at Amber, she's slightly surprised, mouth closed
            with dissolve

            u "What's wrong?"

            scene v11cam45 # TPP. Show Amber kissing MC, he is surprised
            with dissolve

            play sound "sounds/kiss.mp3"

            pause 1.25

            scene v11cam42e
            with dissolve

            clerk "Hey, that's not appropriate for a public-"

            scene v11cam14f
            with dissolve

            am "If you weren't such a dick, maybe someone would kiss you too."

            scene v11cam43a # TPP. Same as v11cam43, Amber pulling MC by the hand this time, MC slight smile, mouth closed, Amber angry, mouth closed
            with dissolve

            pause 0.75

            scene v11cam44 # TPP. Show MC and Amber walking out of the karting place, Amber is angry, mouth closed, MC slight smile, mouth closed
            with dissolve

            pause 0.75

            scene v11cam2f # FPP. Same as v11cam2d, different pose
            with fade

            u "At least we both had a good time, right?"

        "Let her handle it":
            $ add_point(KCT.BRO)
            scene v11cam14f
            with dissolve

            am "Look, pal. I'm not some dumb bitch that you can talk to however you want, I will fuck your ass up if I have to."
            am "You've been hitting on me like a creep since the moment we got here and now you wanna switch it up? Go fuck yourself, bitch boy."

            scene v11cam42c
            with dissolve

            clerk "I-"

            scene v11cam43b # TPP. Same as v11cam43a, but Amber isn't pulling MC by the hand, Amber and MC both angry, mouths closed
            with dissolve

            pause 0.75

            scene v11cam44a # TPP. Same as v11cam44, but both MC and Amber angry, mouths closed
            with dissolve

            pause 0.75

            scene v11cam2d # FPP. Same as v11cam2, Amber slight smile, mouth closed, different pose
            with fade

            u "I can't lie, that was pretty fucking hot."

            scene v11cam2e # FPP. Same as v11cam2d, mouth open
            with dissolve

            am "Not bad for a six, huh?"

            scene v11cam2d
            with dissolve

            u "He doesn't know what the fuck he's talking about, Amber. You know that."

            scene v11cam2f # FPP. Same as v11cam2d, different pose
            with dissolve

            u "At least we both had a good time, right?"

    scene v11cam2g # FPP. Same as v11cam2f, mouth open
    with dissolve

    am "Yeah, I really did."

    scene v11cam2f
    with dissolve

    menu:
        "Tease":
            $ add_point(KCT.TROUBLEMAKER)
            $ v11_tease_amber += 1

            u "You know what, if the job at Lew's doesn't work out, maybe you can work with our friend back there."

            scene v11cam2d
            with dissolve
            
            u "You know, stare into each other's eyes while you countdown for customers. That sounds nice, doesn't it? *Chuckles*"

            scene v11cam2e
            with dissolve

            am "Yeah, sounds like a fucking dream."

            scene v11cam2d
            with dissolve

            u "*Laughs*"

        "Don't tease":
            scene v11cam2d
            with dissolve

            u "Same. That was a lot more fun than I expected it to be, haha."

            scene v11cam2e
            with dissolve

            am "And you almost chickened out... Where would you be without me?"

            scene v11cam2d
            with dissolve

            u "*Chuckles*"
    
    scene v11cam1
    with dissolve

    pause 0.75

    scene v11cam46 # TPP. Show MC and Amber walking into the lobby, both slight smiles, mouths closed
    with fade

    pause 0.75

    scene v11cam47 # TPP. Show MC walking towards where he will be standing in v11car1 (Scene 40) (Show some other students where they are positioned in that scene too)
    with dissolve

    pause 0.75
    stop music fadeout 3
    jump v11_carriage_ride